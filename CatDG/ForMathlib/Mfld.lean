import Mathlib.Geometry.Manifold.ContMDiffMap
import Mathlib.Geometry.Manifold.IsManifold.InteriorBoundary
import Mathlib.Topology.Category.TopCat.Basic

/-!
# The category `Mfld` of manifolds
In this file we define a general category `Mfld ğ•œ n` of all manifolds of a given smoothness degree
`n : WithTop â„•âˆ` over a nontrivially normed ground field `ğ•œ`, imposing no conditions like
Hausdorffness, paracompactness, finite-dimensionality or boundarylessness. We instead set up
these properties as object properties, and define other categories of manifolds as full
subcategories in terms of them.

Currently this is all written with a focus on avoiding boilderplate code: we define each subcategory
as `P.FullSubcategory` for some object property `P`, and provide instances such as
`{P : ObjectProperty (Mfld ğ•œ n)} [Fact (P â‰¤ hausdorff)] (M : P.FullSubcategory) : T2Space M` to
avoid having to set up `T2Space`-instances for each considere subcategory separately. The downsides
of this approach are that dot notation doesn't carry over to full subcategories, and that we
have to define some instances like `[Fact (a â‰¤ c)] : Fact (a âŠ“ b â‰¤ c)`.

## Main definitions / results
* `Mfld ğ•œ n`: the category of all Câ¿ manifolds with corners over a fixed ground field `ğ•œ`.
* `FinDimMfld ğ•œ n`: the category of Hausdorff, paracompact finite-dimensional manifolds without
  boundary, defined as a full subcategory of `Mfld ğ•œ n`.
* `FinDimMfldWCorners ğ•œ n`: the category of Hausdorff, paracompact finite-dimensional manifolds
  with corners, defined as a full subcategory of `Mfld ğ•œ n`.
* `BanachMfld ğ•œ n`: the category of Hausdorff, paracompact Banach manifolds without boundary,
  defined as a full subcategory of `Mfld ğ•œ n`.
* All of these subcategories are concrete.

For each of these subcategories a forgetful functor to `TopCat`, an inclusion into `Mfld ğ•œ n` and
inclusions into other subcategories are provided in the form of `HasForgetâ‚‚`-instances.

## TODOs
* Show that `Mfld ğ•œ n` has all products.
* Show that various object properties are closed under arbitrary / finite products, and conclude
  that the subcategories under consideration also have arbitrary / finite products.
-/

universe u

open CategoryTheory

/-- The category of all (possbily non-Hausdorff, non-paracompact and infinite-dimensional) manifolds
with corners for a fixed ground field `ğ•œ` and smoothness degree `n : WithTop â„•âˆ`.
The main purpose of this category is to act as an ambient category for nicer categories of manifolds
to be considered as full subcategories of. -/
structure Mfld (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] (n : WithTop â„•âˆ) where
  carrier : Type u
  [topology : TopologicalSpace carrier]
  {modelVectorSpace : Type u}
  [normedAddCommGroup : NormedAddCommGroup modelVectorSpace]
  [normedSpace : NormedSpace ğ•œ modelVectorSpace]
  {model : Type u}
  [modelTopology : TopologicalSpace model]
  modelWithCorners : ModelWithCorners ğ•œ modelVectorSpace model
  [chartedSpace : ChartedSpace model carrier]
  [isManifold : IsManifold modelWithCorners n carrier]

attribute [instance] Mfld.topology Mfld.normedAddCommGroup Mfld.normedSpace
  Mfld.modelTopology Mfld.chartedSpace Mfld.isManifold

namespace Mfld

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {n : WithTop â„•âˆ}

instance : CoeSort (Mfld ğ•œ n) (Type u) :=
  âŸ¨Mfld.carrierâŸ©

instance (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] (n : WithTop â„•âˆ) : Category (Mfld ğ•œ n) where
  Hom M N := ContMDiffMap M.modelWithCorners N.modelWithCorners M.carrier N.carrier n
  id M := ContMDiffMap.id
  comp f g := g.comp f

instance : ConcreteCategory.{u} (Mfld ğ•œ n)
    (fun M N => ContMDiffMap M.modelWithCorners N.modelWithCorners M N n) where
  hom f := f
  ofHom f := f

@[simps]
instance : HasForgetâ‚‚ (Mfld ğ•œ n) TopCat where
  forgetâ‚‚ := { obj M := .of M, map (f : ContMDiffMap _ _ _ _ _) := TopCat.ofHom f }

/-- The object property satisfied by all manifolds whose underlying topological space is T2. -/
def hausdorff : ObjectProperty (Mfld ğ•œ n) :=
  fun M â†¦ T2Space M

/-- The object property satisfied by all Ïƒ-compact manifolds. -/
def sigmaCompact : ObjectProperty (Mfld ğ•œ n) :=
  fun M â†¦ SigmaCompactSpace M

/-- The object property satisfied by all manifolds that are boundaryless in the sense of
`BoundarylessManifold`. Note that such manifolds can still be modelled on non-boundaryless
models with corners, they just need to consist entirely of interior points. -/
def boundaryless : ObjectProperty (Mfld ğ•œ n) :=
  fun M â†¦ BoundarylessManifold M.modelWithCorners M

/-- The object property satisfied by all manifolds whose model vector space is complete. -/
def banach : ObjectProperty (Mfld ğ•œ n) :=
  fun M â†¦ CompleteSpace M.modelVectorSpace

/-- The object property satisfied by all manifolds whose model vector space is
finite-dimensional. -/
def finiteDimensional : ObjectProperty (Mfld ğ•œ n) :=
  fun M â†¦ FiniteDimensional ğ•œ M.modelVectorSpace

lemma finiteDimensional_le_banach [CompleteSpace ğ•œ] :
    finiteDimensional (ğ•œ := ğ•œ) (n := n) â‰¤ banach :=
  fun _ (_ : FiniteDimensional ğ•œ _) â†¦ FiniteDimensional.complete ğ•œ _

/-- The object property corresponding to Hausdorff, sigma-compact and finite-dimensional manifolds
without boundary. -/
abbrev finDimMfld : ObjectProperty (Mfld ğ•œ n) :=
  hausdorff âŠ“ sigmaCompact âŠ“ boundaryless âŠ“ finiteDimensional

/-- The object property corresponding to Hausdorff, sigma-compact and finite-dimensional manifolds
with corners. -/
abbrev finDimMfldWCorners : ObjectProperty (Mfld ğ•œ n) :=
  hausdorff âŠ“ sigmaCompact âŠ“ finiteDimensional

/-- The object property corresponding to Hausdorff sigma-compact Banach manifolds
without boundary. -/
abbrev banachMfld : ObjectProperty (Mfld ğ•œ n) :=
  hausdorff âŠ“ sigmaCompact âŠ“ boundaryless âŠ“ banach

lemma finDimMfld_le_finDimMfldWCorners : finDimMfld (ğ•œ := ğ•œ) (n := n) â‰¤ finDimMfldWCorners :=
  inf_le_inf_right _ <| inf_le_left

lemma finDimMfld_le_banachMfld [CompleteSpace ğ•œ] : finDimMfld (ğ•œ := ğ•œ) (n := n) â‰¤ banachMfld :=
  inf_le_inf_left _ finiteDimensional_le_banach

/-- The category of (Hausdorff, paracompact) finite-dimensional manifolds without boundary,
defined as a full subcategory of `Mfld ğ•œ n`. -/
abbrev _root_.FinDimMfld (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] (n : WithTop â„•âˆ) :=
  finDimMfld.FullSubcategory (C := Mfld.{u} ğ•œ n)

/-- The category of (Hausdorff, paracompact) finite-dimensional manifolds with corners,
defined as a full subcategory of `Mfld ğ•œ n`. -/
abbrev _root_.FinDimMfldWCorners (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] (n : WithTop â„•âˆ) :=
  finDimMfldWCorners.FullSubcategory (C := Mfld.{u} ğ•œ n)

/-- The category of (Hausdorff, paracompact) Banach manifolds without boundary,
defined as a full subcategory of `Mfld ğ•œ n`. -/
abbrev _root_.BanachMfld (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] (n : WithTop â„•âˆ) :=
  banachMfld.FullSubcategory (C := Mfld.{u} ğ•œ n)

/-- Each subcategory defined in this way automatically carries the structure of a concrete category
and a forgetful functor to `(Mfld ğ•œ n)`. -/
example : HasForgetâ‚‚ (FinDimMfld ğ•œ n) (Mfld ğ•œ n) := inferInstance

-- TODO: move this somewhere else
instance {C : Type*} [Category C] [CoeSort C (Type u)] (P : ObjectProperty C) :
    CoeSort P.FullSubcategory (Type u) :=
  âŸ¨fun X â†¦ X.objâŸ©

example : CoeSort (FinDimMfld ğ•œ n) (Type u) := inferInstance

-- TODO: move this somewhere else
instance {Î± : Type u} [SemilatticeInf Î±] {a : Î±} : Fact (a â‰¤ a) := âŸ¨le_refl aâŸ©

-- TODO: move this somewhere else
instance {Î± : Type u} [SemilatticeInf Î±] {a b c : Î±} [Fact (a â‰¤ c)] : Fact (a âŠ“ b â‰¤ c) :=
  âŸ¨inf_le_of_left_le Fact.outâŸ©

-- TODO: move this somewhere else
instance {Î± : Type u} [SemilatticeInf Î±] {a b c : Î±} [Fact (b â‰¤ c)] : Fact (a âŠ“ b â‰¤ c) :=
  âŸ¨inf_le_of_right_le Fact.outâŸ©

instance {P : ObjectProperty (Mfld ğ•œ n)} [Fact (P â‰¤ hausdorff)] (M : P.FullSubcategory) :
    T2Space M :=
  (Fact.out : P â‰¤ hausdorff) _ M.property

instance {P : ObjectProperty (Mfld ğ•œ n)} [Fact (P â‰¤ sigmaCompact)] (M : P.FullSubcategory) :
    SigmaCompactSpace M :=
  (Fact.out : P â‰¤ sigmaCompact) _ M.property

instance {P : ObjectProperty (Mfld ğ•œ n)} [Fact (P â‰¤ boundaryless)] (M : P.FullSubcategory) :
    BoundarylessManifold M.obj.modelWithCorners M :=
  (Fact.out : P â‰¤ boundaryless) _ M.property

instance {P : ObjectProperty (Mfld ğ•œ n)} [Fact (P â‰¤ banach)] (M : P.FullSubcategory) :
    CompleteSpace M.obj.modelVectorSpace :=
  (Fact.out : P â‰¤ banach) _ M.property

instance {P : ObjectProperty (Mfld ğ•œ n)} [Fact (P â‰¤ finiteDimensional)] (M : P.FullSubcategory) :
    FiniteDimensional ğ•œ M.obj.modelVectorSpace :=
  (Fact.out : P â‰¤ finiteDimensional) _ M.property

/-- Every object of one of these subcategories automatically receives all the correct instances. -/
example (M : FinDimMfld ğ•œ n) : T2Space M := inferInstance

-- TODO: move this somewhere else
@[simps]
instance {C D : Type*} [Category C] [Category D] [HasForget.{u} C] [HasForget.{u} D]
    [HasForgetâ‚‚ C D] (P : ObjectProperty C) : HasForgetâ‚‚ P.FullSubcategory D :=
  âŸ¨forgetâ‚‚ _ C â‹™ forgetâ‚‚ C D, by simp [Functor.assoc, HasForgetâ‚‚.forget_comp]âŸ©

/-- Each of these subcategories automatically also carries a forgetful functor to `TopCat`. -/
example : HasForgetâ‚‚ (FinDimMfld ğ•œ n) TopCat := inferInstance

-- TODO: move this somewhere else, get `@[simps]` to work
instance {C : Type*} [Category C] [HasForget.{u} C] (P : ObjectProperty C) (Q : ObjectProperty C)
    [Fact (P â‰¤ Q)] : HasForgetâ‚‚ P.FullSubcategory Q.FullSubcategory :=
  âŸ¨P.Î¹OfLE Fact.out, rflâŸ©

instance : Fact (finDimMfld (ğ•œ := ğ•œ) (n := n) â‰¤ finDimMfldWCorners) :=
  âŸ¨finDimMfld_le_finDimMfldWCornersâŸ©

instance [CompleteSpace ğ•œ] : Fact (finDimMfld â‰¤ banachMfld (ğ•œ := ğ•œ) (n := n)) :=
  âŸ¨finDimMfld_le_banachMfldâŸ©

/-- We have also have forgetful functors between the different subcategories. -/
example : HasForgetâ‚‚ (FinDimMfld ğ•œ n) (FinDimMfldWCorners ğ•œ n) := inferInstance

example [CompleteSpace ğ•œ] : HasForgetâ‚‚ (FinDimMfld ğ•œ n) (BanachMfld ğ•œ n) := inferInstance

end Mfld
