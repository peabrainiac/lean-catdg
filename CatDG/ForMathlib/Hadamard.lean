import Mathlib.Analysis.Calculus.ContDiff.Operations
import Mathlib.Analysis.Calculus.LineDeriv.Basic
import Mathlib.Analysis.Calculus.ParametricIntervalIntegral
import Mathlib.MeasureTheory.Integral.IntervalIntegral.FundThmCalculus

/-!
# Hadamard's lemma
Here we prove variants of Hadamard's lemma, stating that a smooth function `f : E â†’ F` between
sufficiently nice vector spaces can for any point `x` and basis `b` be written as
`y â†¦ f x + âˆ‘ i, b.repr (y - x) i â€¢ g i y` where `b.repr (y - x) i` is the `i`-th component of
`y - x` in the basis `b` and each `g i` is a smooth function `E â†’ F`. We do this by providing
explicit functions `hadamardFun f x (b i)` to serve as the `g i`, characterising their smoothness
and showing that `f` can be written in terms of them.

The functions we consider are specifically functions from finite-dimensional spaces to Banach spaces
that are continuously differentiable on an open star-convex set.

See https://en.wikipedia.org/wiki/Hadamard%27s_lemma and https://ncatlab.org/nlab/show/Hadamard+lemma.
-/

universe u

open ContDiff

open scoped Interval

variable {E F : Type u} [NormedAddCommGroup E] [NormedAddCommGroup F]
  [NormedSpace â„ E] [NormedSpace â„ F]

lemma ContinuousOn.intervalIntegral {X : Type*} [TopologicalSpace X] {Î¼ : MeasureTheory.Measure â„}
    [MeasureTheory.NoAtoms Î¼] [MeasureTheory.IsLocallyFiniteMeasure Î¼] {f : X Ã— â„ â†’ E} {u : Set X}
    {aâ‚€ bâ‚€ : â„} (hf : ContinuousOn f (u Ã—Ë¢ [[aâ‚€, bâ‚€]])) :
    ContinuousOn (fun x â†¦ âˆ« t in aâ‚€..bâ‚€, f (x, t) âˆ‚Î¼) u := by
  wlog hab : aâ‚€ â‰¤ bâ‚€ with h
  Â· simp_rw [intervalIntegral.integral_symm bâ‚€ aâ‚€]
    exact (h (Set.uIcc_comm aâ‚€ bâ‚€ â–¸ hf) (le_of_not_ge hab)).neg
  Â· rw [Set.uIcc_of_le hab] at hf
    rw [continuousOn_iff_continuous_restrict] at hf âŠ¢
    replace hf :
        Continuous (Function.uncurry fun (x : u) (t : â„) â†¦ f (x, Set.projIcc _ _ hab t)) :=
      (hf.comp (f := (Homeomorph.Set.prod u _).symm âˆ˜ Prod.map id (Set.projIcc _ _ hab))
        (by fun_prop)).congr fun (x, t) â†¦ by simp
    refine (intervalIntegral.continuous_parametric_intervalIntegral_of_continuous' hf aâ‚€ bâ‚€).congr
      fun x â†¦ intervalIntegral.integral_congr fun t ht â†¦ ?_
    simp [Set.projIcc_of_mem hab <| Set.uIcc_of_le hab â–¸ ht]

section

open Filter Topology Set

variable {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]

/-- The "neighbourhood within" filter for sets. Elements of `ğ“[t] s` are sets containing the
intersection of `t` and a neighbourhood of `s`. -/
def nhdsSetWithin (s t : Set X) : Filter X := ğ“Ë¢ s âŠ“ ğ“Ÿ t

@[inherit_doc]
scoped[Topology] notation "ğ“Ë¢[" t "] " s:100 => nhdsSetWithin s t

@[gcongr, mono]
lemma nhdsSetWithin_mono_left {s s' t : Set X} (h : s âŠ† s') : ğ“Ë¢[t] s â‰¤ ğ“Ë¢[t] s' :=
  inf_le_inf_right _ <| nhdsSet_mono h

@[gcongr, mono]
lemma nhdsSetWithin_mono_right {s t t' : Set X} (h : t âŠ† t') : ğ“Ë¢[t] s â‰¤ ğ“Ë¢[t'] s :=
  inf_le_inf_left _ <| principal_mono.2 h

lemma nhdsSetWithin_hasBasis {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s' : Î¹ â†’ Set X} {s : Set X}
    (h : (ğ“Ë¢ s).HasBasis p s') (t : Set X) : (ğ“Ë¢[t] s).HasBasis p fun i => s' i âˆ© t :=
  h.inf_principal t

lemma nhdsSetWithin_basis_open (s t : Set X) :
    (ğ“Ë¢[t] s).HasBasis (fun u => IsOpen u âˆ§ s âŠ† u) fun u => u âˆ© t :=
  nhdsSetWithin_hasBasis (hasBasis_nhdsSet s) t

lemma mem_nhdsSetWithin {s t u : Set X} : u âˆˆ ğ“Ë¢[t] s â†” âˆƒ v, IsOpen v âˆ§ s âŠ† v âˆ§ v âˆ© t âŠ† u := by
  simpa [and_assoc] using (nhdsSetWithin_basis_open s t).mem_iff

@[simp]
lemma nhdsSetWithin_singleton {x : X} {s : Set X} : ğ“Ë¢[s] {x} = ğ“[s] x := by
  simp [nhdsSetWithin, nhdsWithin]

@[simp]
lemma nhdsSetWithin_univ {s : Set X} : ğ“Ë¢[univ] s = ğ“Ë¢ s := by
  simp [nhdsSetWithin]

@[simp]
lemma nhdsSetWithin_self {s : Set X} : ğ“Ë¢[s] s = ğ“Ÿ s := by
  simp [nhdsSetWithin, principal_le_nhdsSet]

lemma ContinuousOn.preimage_mem_nhdsSetWithin {f : X â†’ Y} {s : Set X}
    (hf : ContinuousOn f s) {t u t' : Set Y} (h : u âˆˆ ğ“Ë¢[t'] t) :
    f â»Â¹' u âˆˆ ğ“Ë¢[s âˆ© f â»Â¹' t'] (s âˆ© f â»Â¹' t) := by
  have âŸ¨v, hvâŸ© := mem_nhdsSetWithin.1 h
  have âŸ¨w, hwâŸ© := continuousOn_iff'.1 hf v hv.1
  refine mem_nhdsSetWithin.2 âŸ¨w, hw.1, ?_, ?_âŸ©
  Â· exact (inter_comm _ _).trans_subset <| (inter_subset_inter_left _ <| preimage_mono hv.2.1).trans
      (hw.2.trans_subset inter_subset_left)
  Â· rw [â† inter_assoc, â† hw.2, inter_comm _ s, inter_assoc, â† preimage_inter]
    exact inter_subset_right.trans <| preimage_mono hv.2.2

/-- If `f` is continuous on `s` and `u` is a neighbourhood of `t`, then `f â»Â¹' u` is a neighbourhood
of `s âˆ© f â»Â¹' t` within `s`. -/
lemma ContinuousOn.preimage_mem_nhdsSetWithin_of_mem_nhdsSet {f : X â†’ Y} {s : Set X}
    (hf : ContinuousOn f s) {t u : Set Y} (h : u âˆˆ ğ“Ë¢ t) : f â»Â¹' u âˆˆ ğ“Ë¢[s] (s âˆ© f â»Â¹' t) := by
  simpa [h] using ContinuousOn.preimage_mem_nhdsSetWithin hf (t := t) (u := u) (t' := univ)

lemma nhdsSetWithin_prod_le {s s' : Set X} {t t' : Set Y} :
    ğ“Ë¢[s' Ã—Ë¢ t'] (s Ã—Ë¢ t) â‰¤ ğ“Ë¢[s'] s Ã—Ë¢ ğ“Ë¢[t'] t := by
  simpa [nhdsSetWithin, â† prod_inf_prod] using inf_le_of_left_le <| nhdsSet_prod_le _ _

lemma IsCompact.nhdsSetWithin_prod_eq {s s' : Set X} {t t' : Set Y} (hs : IsCompact s)
    (ht : IsCompact t) : ğ“Ë¢[s' Ã—Ë¢ t'] (s Ã—Ë¢ t) = ğ“Ë¢[s'] s Ã—Ë¢ ğ“Ë¢[t'] t := by
  simp [nhdsSetWithin, â† prod_inf_prod, hs.nhdsSet_prod_eq ht]

end

open Topology Set in
/-- Variant of `generalized_tube_lemma` in terms of `nhdsSetWithin`. -/
lemma generalized_tube_lemma' {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    {s s' : Set X} (hs : IsCompact s) {t t' : Set Y} (ht : IsCompact t) {n : Set (X Ã— Y)}
    (hn : n âˆˆ ğ“Ë¢[s' Ã—Ë¢ t'] (s Ã—Ë¢ t)) :
    âˆƒ u âˆˆ ğ“Ë¢[s'] s, âˆƒ v âˆˆ ğ“Ë¢[t'] t, u Ã—Ë¢ v âŠ† n := by
  rwa [hs.nhdsSetWithin_prod_eq ht, Filter.mem_prod_iff] at hn

open Topology Set in
/-- Variant of `generalized_tube_lemma` that only replaces the set in one direction. -/
lemma generalized_tube_lemma_left {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    {s s' : Set X} (hs : IsCompact s) {t : Set Y} (ht : IsCompact t) {n : Set (X Ã— Y)}
    (hn : n âˆˆ ğ“Ë¢[s' Ã—Ë¢ t] (s Ã—Ë¢ t)) :
    âˆƒ u âˆˆ ğ“Ë¢[s'] s, u Ã—Ë¢ t âŠ† n := by
  rw [hs.nhdsSetWithin_prod_eq ht, nhdsSetWithin_self, Filter.mem_prod_principal] at hn
  exact âŸ¨_, hn, fun x hx â†¦ hx.1 _ hx.2âŸ©

open Topology Set in
/-- Variant of `generalized_tube_lemma` that only replaces the set in one direction. -/
lemma generalized_tube_lemma_right {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    {s : Set X} (hs : IsCompact s) {t t' : Set Y} (ht : IsCompact t) {n : Set (X Ã— Y)}
    (hn : n âˆˆ ğ“Ë¢[s Ã—Ë¢ t'] (s Ã—Ë¢ t)) :
    âˆƒ u âˆˆ ğ“Ë¢[t'] t, s Ã—Ë¢ u âŠ† n := by
  rw [hs.nhdsSetWithin_prod_eq ht, nhdsSetWithin_self, Filter.mem_prod_iff] at hn
  obtain âŸ¨s', hs', u, hu, hâŸ© := hn
  exact âŸ¨u, hu, (prod_mono_left hs').trans hâŸ©

open TopologicalSpace MeasureTheory Filter Topology Interval Set in
/-- A convenient special case of `intervalIntegral.hasFDerivAt_integral_of_dominated_of_fderiv_le`:
if `f : H Ã— â„ â†’ E` is continuously differentiable on `u Ã—Ë¢ [[a, b]]` for a neighbourhood `u`
of `xâ‚€`, then a derivative of `fun x => âˆ« t in a..b, f (x, t) âˆ‚Î¼` in `xâ‚€` can be computed as
`âˆ« t in a..b, fderiv â„ (fun x â†¦ f (x, t)) xâ‚€ âˆ‚Î¼`. -/
nonrec theorem intervalIntegral.hasFDerivAt_integral_of_contDiffOn
    {Î¼ : Measure â„} [IsLocallyFiniteMeasure Î¼] {E : Type*} [NormedAddCommGroup E]
    [NormedSpace â„ E] [NormedSpace â„ E] {H : Type*} [NormedAddCommGroup H]
    [NormedSpace â„ H] {f : H Ã— â„ â†’ E} {xâ‚€ : H} {u : Set H} (hu : u âˆˆ ğ“ xâ‚€) {a b : â„}
    (hF : ContDiffOn â„ 1 f (u Ã—Ë¢ [[a, b]])) :
    HasFDerivAt (fun x => âˆ« t in a..b, f (x, t) âˆ‚Î¼)
      (âˆ« t in a..b, fderiv â„ (fun x â†¦ f (x, t)) xâ‚€ âˆ‚Î¼) xâ‚€ := by
  wlog hab : a < b with h
  Â· obtain hab | hab := lt_or_eq_of_le <| le_of_not_gt hab
    Â· simp_rw [intervalIntegral.integral_symm b a]
      exact (h (Î¼ := Î¼) hu (Set.uIcc_comm a b â–¸ hF) hab).neg
    Â· simp [hab, hasFDerivAt_const]
  rw [uIcc_of_le hab.le] at hF
  replace âŸ¨u, hu, hxu, hFâŸ© : âˆƒ u, IsOpen u âˆ§ xâ‚€ âˆˆ u âˆ§ ContDiffOn â„ 1 f (u Ã—Ë¢ Icc a b) := by
    have âŸ¨u', hu'âŸ© := mem_nhds_iff.1 hu
    exact âŸ¨u', hu'.2.1, hu'.2.2, hF.mono <| prod_mono_left hu'.1âŸ©
  let F' := fun x : H Ã— â„ â†¦ fderiv â„ (fun y â†¦ f (y, x.2)) x.1
  have hF' : ContinuousOn F' (u Ã—Ë¢ Icc a b) := by
    refine .congr (f := fun x â†¦ (fderivWithin â„ f (u Ã—Ë¢ Set.Icc a b) x).comp (.inl â„ H â„))
      ?_ fun x hx â†¦ ?_
    Â· refine ((ContinuousLinearMap.compL â„ H (H Ã— â„) E).flip
        (.inl â„ H â„)).continuous.comp_continuousOn ?_
      refine (hF.continuousOn_fderivWithin ?_ le_rfl)
      exact hu.uniqueDiffOn.prod <| uniqueDiffOn_Icc hab
    Â· dsimp [F']; rw [show (fun y â†¦ f (y, x.2)) = (f âˆ˜ fun y â†¦ (y, x.2)) by rfl]
      rw [â† fderivWithin_eq_fderiv (s := u) (hu.uniqueDiffWithinAt hx.1) <| by
        refine DifferentiableOn.differentiableAt (s := u) ?_ (hu.mem_nhds hx.1)
        exact ((hF.differentiableOn le_rfl).comp (by fun_prop) (fun y hy â†¦ âŸ¨hy, hx.2âŸ©))]
      rw [fderivWithin_comp _ (t := u Ã—Ë¢ Set.Icc a b) (hF.differentiableOn (by simp) _ âŸ¨hx.1, hx.2âŸ©)
        (by fun_prop) (by exact fun y hy â†¦ âŸ¨hy, hx.2âŸ©) (hu.uniqueDiffWithinAt hx.1)]
      congr
      exact (hasFDerivAt_prodMk_left _ x.2).hasFDerivWithinAt.fderivWithin
        (hu.uniqueDiffWithinAt hx.1)
  let F'' := fun x â†¦ â€–F' xâ€–
  have hF'' : ContinuousOn F'' _ := continuous_norm.comp_continuousOn hF'
  let âŸ¨Îµ, hÎµ, hÎµ', B, hBâŸ© :
      âˆƒ Îµ > 0, Metric.ball xâ‚€ Îµ âŠ† u âˆ§ âˆƒ B, âˆ€ x âˆˆ Metric.ball xâ‚€ Îµ Ã—Ë¢ Icc a b, F'' x < B := by
    let âŸ¨B, hBâŸ© := (isCompact_singleton.prod isCompact_Icc).bddAbove_image <|
      hF''.mono <| prod_mono_left <| singleton_subset_iff.2 hxu
    have âŸ¨v, hv, hv'âŸ© := generalized_tube_lemma_left (s := {xâ‚€}) isCompact_singleton
      (t := Icc a b) isCompact_Icc (s' := u) (n := F'' â»Â¹' (Iio (B + 1))) (by
        refine nhdsSetWithin_mono_left ?_ <| hF''.preimage_mem_nhdsSetWithin_of_mem_nhdsSet
          (t := Iic B) (u := Iio (B + 1)) <| isOpen_Iio.mem_nhdsSet.2 (by simp)
        intro x hx
        exact âŸ¨prod_mono_left (by simp [hxu]) hx, mem_upperBounds.1 hB _ <| mem_image_of_mem _ hxâŸ©)
    rw [nhdsSetWithin_singleton, hu.nhdsWithin_eq hxu] at hv
    have âŸ¨Îµ, hÎµ, hÎµ'âŸ© := Metric.mem_nhds_iff.1 (Filter.inter_mem hv (hu.mem_nhds hxu))
    exact âŸ¨Îµ, hÎµ, hÎµ'.trans inter_subset_right, B + 1,
      fun x hx â†¦ hv' <| prod_mono_left (hÎµ'.trans inter_subset_left) hxâŸ©
  refine intervalIntegral.hasFDerivAt_integral_of_dominated_of_fderiv_le (bound := fun _ â†¦ B)
    (F' := fun x t â†¦ fderiv â„ (fun x â†¦ f (x, t)) x) hÎµ ?_ ?_ ?_ ?_ ?_ ?_
  Â· refine eventually_nhds_iff.2 âŸ¨u, fun x hx â†¦ ?_, hu, hxuâŸ©
    refine ContinuousOn.aestronglyMeasurable ?_ measurableSet_uIoc
    refine .mono ?_ <| (uIoc_of_le hab.le).trans_le Ioc_subset_Icc_self
    exact hF.continuousOn.comp (by fun_prop) fun t ht â†¦ âŸ¨hx, htâŸ©
  Â· apply ContinuousOn.intervalIntegrable
    exact hF.continuousOn.comp (by fun_prop) fun t ht â†¦ âŸ¨hxu, uIcc_of_le hab.le â–¸ htâŸ©
  Â· refine ContinuousOn.aestronglyMeasurable ?_ measurableSet_uIoc
    refine .mono ?_ <| (uIoc_of_le hab.le).trans_le Ioc_subset_Icc_self
    exact hF'.comp (f := fun t â†¦ (xâ‚€, t)) (by fun_prop) fun t ht â†¦ âŸ¨hxu, htâŸ©
  Â· refine .of_forall fun t ht x hx â†¦ ?_
    exact (hB (x, t) âŸ¨hx, Ioc_subset_Icc_self <| uIoc_of_le hab.le â–¸ htâŸ©).le
  Â· exact intervalIntegrable_const
  Â· refine .of_forall fun t ht x hx â†¦ ?_
    refine (DifferentiableOn.differentiableAt ?_ (hu.mem_nhds <| hÎµ' hx)).hasFDerivAt
    exact hF.differentiableOn_one.comp (by fun_prop) fun x hx â†¦
      âŸ¨hx, Ioc_subset_Icc_self <| uIoc_of_le hab.le â–¸ htâŸ©

lemma ContDiffOn.intervalIntegral {Î¼ : MeasureTheory.Measure â„}
    [MeasureTheory.IsLocallyFiniteMeasure Î¼] [MeasureTheory.NoAtoms Î¼]
    {f : E Ã— â„ â†’ F} {u : Set E} (hu : IsOpen u) {a b : â„} {n : â„•âˆ}
    (hf : ContDiffOn â„ n f (u Ã—Ë¢ [[a, b]])) :
    ContDiffOn â„ n (fun x â†¦ âˆ« t in a..b, f (x, t) âˆ‚Î¼) u := by
  wlog hab : a < b with h
  Â· obtain hab | hab := lt_or_eq_of_le <| le_of_not_gt hab
    Â· simp_rw [intervalIntegral.integral_symm b a]
      exact (h hu (Set.uIcc_comm a b â–¸ hf) hab).neg
    Â· simp [hab, contDiffOn_const]
  revert F; change âˆ€ F : _, _
  refine ENat.nat_induction n ?_ ?_ ?_
  Â· intro F _ _ f
    simp_rw [WithTop.coe_zero, contDiffOn_zero]
    exact ContinuousOn.intervalIntegral
  Â· intro k h F _ _ f hf
    refine (contDiffOn_succ_iff_fderiv_of_isOpen (ğ•œ := â„) (n := k) hu).2 âŸ¨?_, by simp, ?_âŸ©
    Â· intro x hx
      have h := intervalIntegral.hasFDerivAt_integral_of_contDiffOn (Î¼ := Î¼)
        (hu.mem_nhds hx) (hf.of_le <| by simp)
      exact h.differentiableAt.differentiableWithinAt
    Â· have := hf.fderivWithin (hu.uniqueDiffOn.prod <| Set.uIcc_of_le hab.le â–¸ uniqueDiffOn_Icc hab)
        (m := k) le_rfl
      refine (h _ (f := fun x â†¦ (fderivWithin â„ f (u Ã—Ë¢ [[a, b]]) x).comp (.inl â„ E â„))
        (by fun_prop)).congr ?_
      intro x hx
      have h := intervalIntegral.hasFDerivAt_integral_of_contDiffOn (Î¼ := Î¼)
        (hu.mem_nhds hx) (hf.of_le <| by simp)
      rw [h.fderiv]
      refine intervalIntegral.integral_congr fun t ht â†¦ ?_
      rw [show (fun x â†¦ f (x, t)) = (f âˆ˜ fun x â†¦ (x, t)) by rfl]
      rw [â† fderivWithin_eq_fderiv (hu.uniqueDiffWithinAt hx) (((hf.differentiableOn (by simp)).comp
        (by fun_prop) (fun x hx â†¦ âŸ¨hx, htâŸ©)).differentiableAt (hu.mem_nhds hx))]
      rw [fderivWithin_comp _ (t := u Ã—Ë¢ [[a, b]]) (hf.differentiableOn (by simp) _ âŸ¨hx, htâŸ©)
        (by fun_prop) (fun x hx â†¦ âŸ¨hx, htâŸ©) (hu.uniqueDiffWithinAt hx)]
      congr
      exact (hasFDerivAt_prodMk_left x t).hasFDerivWithinAt.fderivWithin (hu.uniqueDiffWithinAt hx)
  Â· intro h F _ _ f hf
    exact contDiffOn_infty.2 fun n â†¦ h n F <| hf.of_le <| WithTop.coe_le_coe.2 le_top

/-- The function appearing in Hadamard's lemma applied to the function `f` at `x` for a basis
vector `b`. -/
noncomputable def hadamardFun (f : E â†’ F) (x b : E) : E â†’ F :=
    fun y â†¦ âˆ« t in (0 : â„)..1, lineDeriv â„ f (x + t â€¢ (y - x)) b

protected lemma ContDiffOn.hadamardFun {x : E} {s : Set E} (hs : IsOpen s) (hs' : StarConvex â„ x s)
    {f : E â†’ F} {n m : â„•âˆ} (hf : ContDiffOn â„ n f s) (hm : m + 1 â‰¤ n) (b : E) :
    ContDiffOn â„ m (hadamardFun f x b) s := by
  unfold hadamardFun
  refine ContDiffOn.intervalIntegral (f := fun y â†¦ lineDeriv â„ f (x + y.2 â€¢ (y.1 - x)) b) hs ?_
  rw [Set.uIcc_of_le zero_le_one]
  refine .congr ?_ (fun y hy â†¦ DifferentiableAt.lineDeriv_eq_fderiv <|
    (hf.differentiableOn <| by simp [le_of_add_le_right hm]).differentiableAt <|
      hs.mem_nhds <| hs'.add_smul_sub_mem hy.1 hy.2.1 hy.2.2)
  refine .mono ?_ (s := (fun y : E Ã— â„ â†¦ x + y.2 â€¢ (y.1 - x)) â»Â¹' s)
    fun y hy â†¦ hs'.add_smul_sub_mem hy.1 hy.2.1 hy.2.2
  exact (ContinuousLinearMap.apply â„ F b).contDiff.comp_contDiffOn <|
    (hf.fderiv_of_isOpen hs <| WithTop.coe_le_coe.2 hm).comp (by fun_prop) <| s.mapsTo_preimage _

protected lemma ContDiff.hadamardFun {f : E â†’ F} {n m : â„•âˆ} (hf : ContDiff â„ n f)
    (hm : m + 1 â‰¤ n) (x b : E) : ContDiff â„ m (hadamardFun f x b) :=
  contDiffOn_univ.1 <| (contDiffOn_univ.2 hf).hadamardFun isOpen_univ (starConvex_univ x) hm b

variable [CompleteSpace F]

open intervalIntegral in
lemma eqOn_add_sum_hadamardFun {x : E} {s : Set E} (hs : IsOpen s) (hs' : StarConvex â„ x s)
    {f : E â†’ F} {n : WithTop â„•âˆ} (hf : ContDiffOn â„ n f s) (hn : 1 â‰¤ n)
    {Î¹ : Type*} [Fintype Î¹] (b : Module.Basis Î¹ â„ E) :
    s.EqOn f (fun y â†¦ f x + âˆ‘ i : Î¹, b.repr (y - x) i â€¢ hadamardFun f x (b i) y) := by
  intro y hy
  have hs'' : âˆ€ t âˆˆ Set.uIcc (0 : â„) 1, x + t â€¢ (y - x) âˆˆ s := fun t ht â†¦ by
    rw [Set.uIcc_of_le zero_le_one] at ht
    exact hs'.add_smul_sub_mem hy ht.1 ht.2
  refine sub_eq_iff_eq_add'.1 <| Eq.trans (by simp) <| (integral_deriv_eq_sub
    (a := 0) (b := 1) (f := f âˆ˜ fun t â†¦ x + t â€¢ (y - x)) ?_ ?_).symm.trans ?_
  Â· intro t ht
    have := (hf.differentiableOn hn).differentiableAt <| hs.mem_nhds <| hs'' _ ht
    fun_prop
  Â· refine ContinuousOn.intervalIntegrable ?_
    exact ((hf.comp (by fun_prop) <| s.mapsTo_preimage _).continuousOn_deriv_of_isOpen
      (hs.preimage <| by fun_prop) hn).mono fun t ht â†¦ hs'' _ ht
  Â· unfold hadamardFun
    simp_rw [â† integral_smul]
    rw [â† integral_finset_sum]
    Â· refine integral_congr fun t ht â†¦ ?_
      rw [â† fderiv_deriv, fderiv_comp]
      Â· simp_rw [DifferentiableAt.lineDeriv_eq_fderiv <|
          (hf.differentiableOn hn).differentiableAt <| hs.mem_nhds <| hs'' _ ht]
        simp_rw [â† ContinuousLinearMap.map_smul, â† map_sum]
        simp [deriv_smul_const, - map_sub]
      Â· refine (hf.differentiableOn hn).differentiableAt <| hs.mem_nhds <| hs'' _ ht
      Â· simp
    Â· intro i _
      refine (continuousOn_const.smul ?_).intervalIntegrable
      refine .congr ?_ (fun t ht â†¦ DifferentiableAt.lineDeriv_eq_fderiv <|
          (hf.differentiableOn hn).differentiableAt <| hs.mem_nhds <| hs'' _ ht)
      refine .mono ?_  (s := (fun t â†¦ x + t â€¢ (y - x)) â»Â¹' s) fun t ht â†¦ hs'' _ ht
      exact (ContinuousLinearMap.apply â„ F _).continuous.comp_continuousOn <|
        (hf.continuousOn_fderiv_of_isOpen hs hn).comp (by fun_prop) <| s.mapsTo_preimage _

lemma eq_add_sum_hadamardFun {x : E} {f : E â†’ F} {n : WithTop â„•âˆ} (hf : ContDiff â„ n f)
    (hn : 1 â‰¤ n) {Î¹ : Type*} [Fintype Î¹] (b : Module.Basis Î¹ â„ E) :
    f = (fun y â†¦ f x + âˆ‘ i : Î¹, b.repr (y - x) i â€¢ hadamardFun f x (b i) y) := by
  simpa using eqOn_add_sum_hadamardFun isOpen_univ (starConvex_univ x) hf.contDiffOn hn b
