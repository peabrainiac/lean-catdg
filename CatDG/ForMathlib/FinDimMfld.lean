import Mathlib.CategoryTheory.ConcreteCategory.EpiMono
import Mathlib.Geometry.Manifold.ContMDiff.NormedSpace
import Mathlib.Geometry.Manifold.ContMDiff.Atlas
import Mathlib.Geometry.Manifold.IsManifold.InteriorBoundary
import Mathlib.Geometry.Manifold.ContMDiffMap
import Mathlib.Topology.Category.TopCat.Basic

/-!
# The category of finite-dimensional manifolds
We define the category of finite-dimensional, Hausdorff, sigma-compact manifolds without boundary,
for any given smoothness degree `n : WithTop â„•âˆ` and nontrivially normed ground field `ğ•œ`.

So far, the only result we prove to test that the API is set up correctly is that the monomorphisms
in the category of finite-dimensional manifolds are precisely the injective functions.
-/

universe u

open CategoryTheory ConcreteCategory Manifold Function

/-- The category of all finite-dimensional manifolds for a fixed ground field `ğ•œ` and
smoothness degree `n : WithTop â„•âˆ`. Objects are the finite-dimensional sigma-compact Hausdorff
manifolds without boundary, morphisms are the Câ¿ maps. -/
structure FinDimMfld (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] (n : WithTop â„•âˆ) where
  carrier : Type u
  [topology : TopologicalSpace carrier]
  {modelVectorSpace : Type u}
  [normedAddCommGroup : NormedAddCommGroup modelVectorSpace]
  [normedSpace : NormedSpace ğ•œ modelVectorSpace]
  {model : Type u}
  [modelTopology : TopologicalSpace model]
  modelWithCorners : ModelWithCorners ğ•œ modelVectorSpace model
  [chartedSpace : ChartedSpace model carrier]
  [isManifold : IsManifold modelWithCorners n carrier]
  [finiteDimensional : FiniteDimensional ğ•œ modelVectorSpace]
  [t2Space : T2Space carrier]
  [sigmaCompactSpace : SigmaCompactSpace carrier]
  [boundaryless : BoundarylessManifold modelWithCorners carrier]

attribute [instance] FinDimMfld.topology FinDimMfld.normedAddCommGroup FinDimMfld.normedSpace
  FinDimMfld.modelTopology FinDimMfld.chartedSpace FinDimMfld.isManifold
  FinDimMfld.finiteDimensional FinDimMfld.t2Space FinDimMfld.sigmaCompactSpace
  FinDimMfld.boundaryless

initialize_simps_projections FinDimMfld (+carrier, +modelVectorSpace, +model, +modelWithCorners)

namespace FinDimMfld

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {n : WithTop â„•âˆ}

instance : CoeSort (FinDimMfld ğ•œ n) (Type u) :=
  âŸ¨FinDimMfld.carrierâŸ©

instance (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] (n : WithTop â„•âˆ) : Category (FinDimMfld ğ•œ n) where
  Hom M N := ContMDiffMap M.modelWithCorners N.modelWithCorners M.carrier N.carrier n
  id M := ContMDiffMap.id
  comp f g := g.comp f

instance : ConcreteCategory.{u} (FinDimMfld ğ•œ n)
    (fun M N => ContMDiffMap M.modelWithCorners N.modelWithCorners M N n) where
  hom f := f
  ofHom f := f

/-- The forgetful functor from manifolds to topological spaces. -/
def toTopCat : FinDimMfld ğ•œ n â¥¤ TopCat where
  obj M := .of M
  map (f : ContMDiffMap _ _ _ _ _) := TopCat.ofHom f

lemma mono_iff_injective {M N : FinDimMfld.{u} ğ•œ n} (f : M âŸ¶ N) : Mono f â†” Injective f := by
  refine âŸ¨fun hf x y h â†¦ ?_, ConcreteCategory.mono_of_injective _âŸ©
  let pt : FinDimMfld.{u} ğ•œ n := .mk PUnit ğ“˜(ğ•œ, PUnit)
  let x' : pt âŸ¶ M := ofHom (.const x)
  let y' : pt âŸ¶ M := ofHom (.const y)
  exact CategoryTheory.congr_fun (hf.right_cancellation x' y' <| by ext; exact h) default

end FinDimMfld
