import Mathlib.CategoryTheory.ConcreteCategory.EpiMono
import Mathlib.CategoryTheory.Limits.Constructions.FiniteProductsOfBinaryProducts
import Mathlib.Geometry.Manifold.IsManifold.InteriorBoundary
import Mathlib.Geometry.Manifold.PartitionOfUnity
import Mathlib.Topology.Category.TopCat.Basic

/-!
# The category of finite-dimensional manifolds
We define the category of finite-dimensional, Hausdorff, sigma-compact manifolds without boundary,
for any given smoothness degree `n : WithTop â„•âˆ` and nontrivially normed ground field `ğ•œ`.

So far, the only result we prove to test that the API is set up correctly is that the monomorphisms
in the category of finite-dimensional manifolds are precisely the injective functions.

## Main definitions / results
* `FinDimMfld ğ•œ n`: the category of all finite-dimensional Hausdorff Ïƒ-compact Câ¿ manifolds without
  boundary over a fixed ground field `ğ•œ`.
* `FinDimMfld ğ•œ n` has all finite products.
* `FinDimMfld.mono_iff_injective`: morphisms in `FinDimMfld ğ•œ n` are monomorphisms iff they are
  injective
* `FinDimMfld.epi_iff_denseRange`: morphisms in `FinDimMfld â„ âˆ` are epimorphisms iff their range is
  dense

## TODOs
* show that `FinDimMfld ğ•œ n` is essentially small
* generalise `epi_iff_denseRange` to smoothness degrees in `â„•âˆ`
* can anything interesting be said about extremal monomorphisms / epimorphisms?
* define the embedding into `(CommAlgCat â„)áµ’áµ–` and show that it is fully faithful. Fullness will
  likely require a stronger Whitney embedding theorem than currently is in mathlib.
-/

universe u

open CategoryTheory ConcreteCategory Manifold ContDiff Function Limits

/-- The category of all finite-dimensional manifolds for a fixed ground field `ğ•œ` and
smoothness degree `n : WithTop â„•âˆ`. Objects are the finite-dimensional sigma-compact Hausdorff
manifolds without boundary, morphisms are the Câ¿ maps. -/
structure FinDimMfld (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] (n : WithTop â„•âˆ) where
  carrier : Type u
  [topology : TopologicalSpace carrier]
  {modelVectorSpace : Type u}
  [normedAddCommGroup : NormedAddCommGroup modelVectorSpace]
  [normedSpace : NormedSpace ğ•œ modelVectorSpace]
  {model : Type u}
  [modelTopology : TopologicalSpace model]
  modelWithCorners : ModelWithCorners ğ•œ modelVectorSpace model
  [chartedSpace : ChartedSpace model carrier]
  [isManifold : IsManifold modelWithCorners n carrier]
  [finiteDimensional : FiniteDimensional ğ•œ modelVectorSpace]
  [t2Space : T2Space carrier]
  [sigmaCompactSpace : SigmaCompactSpace carrier]
  [boundaryless : BoundarylessManifold modelWithCorners carrier]

attribute [instance] FinDimMfld.topology FinDimMfld.normedAddCommGroup FinDimMfld.normedSpace
  FinDimMfld.modelTopology FinDimMfld.chartedSpace FinDimMfld.isManifold
  FinDimMfld.finiteDimensional FinDimMfld.t2Space FinDimMfld.sigmaCompactSpace
  FinDimMfld.boundaryless

initialize_simps_projections FinDimMfld (+carrier, +modelVectorSpace, +model, +modelWithCorners)

namespace FinDimMfld

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {n : WithTop â„•âˆ}

instance : CoeSort (FinDimMfld ğ•œ n) (Type u) :=
  âŸ¨FinDimMfld.carrierâŸ©

instance (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] (n : WithTop â„•âˆ) : Category (FinDimMfld ğ•œ n) where
  Hom M N := ContMDiffMap M.modelWithCorners N.modelWithCorners M.carrier N.carrier n
  id M := ContMDiffMap.id
  comp f g := g.comp f

instance : ConcreteCategory.{u} (FinDimMfld ğ•œ n)
    (fun M N => ContMDiffMap M.modelWithCorners N.modelWithCorners M N n) where
  hom f := f
  ofHom f := f

@[simps]
instance : HasForgetâ‚‚ (FinDimMfld ğ•œ n) TopCat where
  forgetâ‚‚ := { obj M := .of M, map (f : ContMDiffMap _ _ _ _ _) := TopCat.ofHom f }

/-- A choice of terminal object in the category of manifolds, given by `PUnit`. -/
abbrev pt : FinDimMfld ğ•œ n := .mk PUnit ğ“˜(ğ•œ, PUnit)

/-- The choice `FinDimMfld.pt` of terminal object is indeed terminal. -/
def isTerminalPt : IsTerminal (pt : FinDimMfld ğ•œ n) where
  lift s := ofHom (.const (default : PUnit))

/-- An explicit choice of product in the category of manifolds, given by the product of the
underlying types and models with corners. -/
protected abbrev prod (M N : FinDimMfld.{u} ğ•œ n) : FinDimMfld.{u} ğ•œ n :=
  âŸ¨M Ã— N, M.modelWithCorners.prod N.modelWithCornersâŸ©

/-- The first projection realising `M.prod N` as the product of `M` and `N`. -/
def prodFst {M N : FinDimMfld ğ•œ n} : M.prod N âŸ¶ M := ofHom .fst

/-- The second projection realising `M.prod N` as the product of `M` and `N`. -/
def prodSnd {M N : FinDimMfld ğ•œ n} : M.prod N âŸ¶ N := ofHom .snd

/-- An explicit binary fan of `M` and `N` given by `M.prod N`. -/
def prodBinaryFan (M N : FinDimMfld ğ•œ n) : BinaryFan N M :=
  BinaryFan.mk prodFst prodSnd

/-- The constructed binary fan is indeed a limit. -/
def prodBinaryFanIsLimit (M N : FinDimMfld ğ•œ n) : IsLimit (prodBinaryFan N M) where
  lift c := ofHom <| .prodMk (BinaryFan.fst c) (BinaryFan.snd c)
  fac := by rintro c (_ | _) <;> dsimp [prodBinaryFan, prodFst] <;> ext <;> rfl
  uniq c f h := by
    ext x; refine Prod.ext ?_ ?_
    Â· exact CategoryTheory.congr_fun (h âŸ¨WalkingPair.leftâŸ©) x
    Â· exact CategoryTheory.congr_fun (h âŸ¨WalkingPair.rightâŸ©) x

instance : HasFiniteProducts (FinDimMfld ğ•œ n) := by
  refine @hasFiniteProducts_of_has_binary_and_terminal _ _ ?_ isTerminalPt.hasTerminal
  exact @hasBinaryProducts_of_hasLimit_pair _ _ âŸ¨âŸ¨_, prodBinaryFanIsLimit _ _âŸ©âŸ©

lemma mono_iff_injective {M N : FinDimMfld.{u} ğ•œ n} (f : M âŸ¶ N) : Mono f â†” Injective f := by
  refine âŸ¨fun hf x y h â†¦ ?_, ConcreteCategory.mono_of_injective _âŸ©
  let x' : pt âŸ¶ M := ofHom (.const x)
  let y' : pt âŸ¶ M := ofHom (.const y)
  exact CategoryTheory.congr_fun (hf.right_cancellation x' y' <| by ext; exact h) default

lemma epi_iff_denseRange {M N : FinDimMfld.{0} â„ âˆ} (f : M âŸ¶ N) :
    Epi f â†” DenseRange f := by
  refine âŸ¨not_imp_not.1 fun hf hf' â†¦ ?_, fun hf â†¦ âŸ¨fun g g' hg â†¦ ?_âŸ©âŸ©
  Â· rw [DenseRange, â† compl_compl (Set.range _), â† interior_eq_empty_iff_dense_compl] at hf
    replace hf := Set.nonempty_iff_ne_empty.2 hf
    obtain âŸ¨x, hxâŸ© := hf
    let â„' : FinDimMfld.{0} â„ âˆ:= mk â„ ğ“˜(â„, â„)
    let âŸ¨g, hgâŸ© := exists_smooth_zero_one_of_isClosed N.modelWithCorners
      (isClosed_closure (s := Set.range (hom f))) (isClosed_singleton (x := x)) (by simpa using hx)
    have hg' := hf'.left_cancellation (Z := â„') (ofHom g) (ofHom (.const 0))
      (by ext y; exact congrFun (Set.eqOn_range.1 <| hg.1.mono subset_closure) y)
    exact zero_ne_one ((CategoryTheory.congr_fun hg' x).symm.trans <| hg.2.1 (Set.mem_singleton _))
  Â· apply ConcreteCategory.coe_ext
    rw [â† Set.eqOn_univ, â† hf.closure_range]
    refine Set.EqOn.closure ?_ (map_continuous _) (map_continuous _)
    exact Set.eqOn_range.2 <| congrArg (fun (f : M âŸ¶ _) â†¦ â‡‘(hom f)) hg

end FinDimMfld
