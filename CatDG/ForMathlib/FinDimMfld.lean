import CatDG.Diffeology.Manifolds
import CatDG.ForMathlib.Mfld
import Mathlib.CategoryTheory.ConcreteCategory.EpiMono
import Mathlib.CategoryTheory.Limits.Constructions.FiniteProductsOfBinaryProducts
import Mathlib.Geometry.Manifold.PartitionOfUnity

/-!
# The category of finite-dimensional manifolds
Results on the category `FinDimMfld ğ•œ n` of finite-dimensional, Hausdorff, sigma-compact manifolds
without boundary, for any given smoothness degree `n : WithTop â„•âˆ` and nontrivially normed ground
field `ğ•œ`.

This category is already defined in `CatDG.ForMathlib.Mfld` as a full subcategory of `Mfld ğ•œ n`
and equipped with forgetful functors to various categories there; here we only prove more specific
results.

## Main definitions / results
* `FinDimMfld ğ•œ n`: the category of all finite-dimensional Hausdorff Ïƒ-compact Câ¿ manifolds without
  boundary over a fixed ground field `ğ•œ`.
* `FinDimMfld ğ•œ n` has all finite products.
* `FinDimMfld.mono_iff_injective`: morphisms in `FinDimMfld ğ•œ n` are monomorphisms iff they are
  injective
* `FinDimMfld.epi_iff_denseRange`: morphisms in `FinDimMfld â„ âˆ` are epimorphisms iff their range is
  dense

## TODOs
* show that `FinDimMfld ğ•œ n` is essentially small
* generalise `epi_iff_denseRange` to smoothness degrees in `â„•âˆ`
* can anything interesting be said about extremal monomorphisms / epimorphisms?
* define the embedding into `(CommAlgCat â„)áµ’áµ–` and show that it is fully faithful. Fullness will
  likely require a stronger Whitney embedding theorem than currently is in mathlib.
-/

universe u

open CategoryTheory ConcreteCategory Manifold ContDiff Function Limits TopologicalSpace

initialize_simps_projections Mfld (+carrier, +modelVectorSpace, +model, +modelWithCorners)

namespace FinDimMfld

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {n : WithTop â„•âˆ}

protected abbrev mk' {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {n : WithTop â„•âˆ} (M : Type u)
    [TopologicalSpace M] {E : Type u} [NormedAddCommGroup E] [NormedSpace ğ•œ E] {H : Type u}
    [TopologicalSpace H] (I : ModelWithCorners ğ•œ E H) [ChartedSpace H M] [IsManifold I n M]
    [FiniteDimensional ğ•œ E] [T2Space M] [SigmaCompactSpace M] [BoundarylessManifold I M] :
    FinDimMfld ğ•œ n :=
  âŸ¨âŸ¨M, IâŸ©, âŸ¨âŸ¨â€¹_â€º, â€¹_â€ºâŸ©, â€¹_â€ºâŸ©, â€¹_â€ºâŸ©

/-- A choice of terminal object in the category of manifolds, given by `PUnit`. -/
abbrev pt : FinDimMfld ğ•œ n := .mk' PUnit ğ“˜(ğ•œ, PUnit)

/-- The choice `FinDimMfld.pt` of terminal object is indeed terminal. -/
def isTerminalPt : IsTerminal (pt : FinDimMfld ğ•œ n) where
  lift s := ofHom (.const (default : PUnit))

/-- An explicit choice of product in the category of manifolds, given by the product of the
underlying types and models with corners. -/
protected abbrev prod (M N : FinDimMfld.{u} ğ•œ n) : FinDimMfld.{u} ğ•œ n :=
  .mk' (M Ã— N) (M.obj.modelWithCorners.prod N.obj.modelWithCorners)

/-- The first projection realising `M.prod N` as the product of `M` and `N`. -/
def prodFst {M N : FinDimMfld ğ•œ n} : M.prod N âŸ¶ M := ofHom .fst

/-- The second projection realising `M.prod N` as the product of `M` and `N`. -/
def prodSnd {M N : FinDimMfld ğ•œ n} : M.prod N âŸ¶ N := ofHom .snd

/-- An explicit binary fan of `M` and `N` given by `M.prod N`. -/
def prodBinaryFan (M N : FinDimMfld ğ•œ n) : BinaryFan N M :=
  BinaryFan.mk prodFst prodSnd

/-- The constructed binary fan is indeed a limit. -/
def prodBinaryFanIsLimit (M N : FinDimMfld ğ•œ n) : IsLimit (prodBinaryFan N M) where
  lift c := ofHom <| .prodMk (hom <| BinaryFan.fst c) (hom <| BinaryFan.snd c)
  fac := by rintro c (_ | _) <;> dsimp [prodBinaryFan, prodFst] <;> ext <;> rfl
  uniq c f h := by
    ext x; refine Prod.ext ?_ ?_
    Â· exact CategoryTheory.congr_fun (h âŸ¨WalkingPair.leftâŸ©) x
    Â· exact CategoryTheory.congr_fun (h âŸ¨WalkingPair.rightâŸ©) x

instance : HasFiniteProducts (FinDimMfld ğ•œ n) := by
  refine @hasFiniteProducts_of_has_binary_and_terminal _ _ ?_ isTerminalPt.hasTerminal
  exact @hasBinaryProducts_of_hasLimit_pair _ _ âŸ¨âŸ¨_, prodBinaryFanIsLimit _ _âŸ©âŸ©

-- TODO: figure out how to get this from more general instances
noncomputable instance : Unique (âŠ¤_ (FinDimMfld ğ•œ n)) := by
  have : Unique ((forget (FinDimMfld ğ•œ n)).obj pt) := inferInstanceAs (Unique PUnit)
  exact ((forget _).mapIso (terminalIsTerminal.uniqueUpToIso isTerminalPt)).toEquiv.unique

lemma mono_iff_injective {M N : FinDimMfld.{u} ğ•œ n} (f : M âŸ¶ N) : Mono f â†” Injective f := by
  refine âŸ¨fun hf x y h â†¦ ?_, ConcreteCategory.mono_of_injective _âŸ©
  let x' : pt âŸ¶ M := ofHom (.const x)
  let y' : pt âŸ¶ M := ofHom (.const y)
  exact CategoryTheory.congr_fun (hf.right_cancellation x' y' <| by ext; exact h) default

lemma epi_iff_denseRange {M N : FinDimMfld.{0} â„ âˆ} (f : M âŸ¶ N) :
    Epi f â†” DenseRange f := by
  refine âŸ¨not_imp_not.1 fun hf hf' â†¦ ?_, fun hf â†¦ âŸ¨fun g g' hg â†¦ ?_âŸ©âŸ©
  Â· rw [DenseRange, â† compl_compl (Set.range _), â† interior_eq_empty_iff_dense_compl] at hf
    replace hf := Set.nonempty_iff_ne_empty.2 hf
    obtain âŸ¨x, hxâŸ© := hf
    let â„' : FinDimMfld.{0} â„ âˆ:= .mk' â„ ğ“˜(â„, â„)
    let âŸ¨g, hgâŸ© := exists_contMDiffMap_zero_one_of_isClosed N.obj.modelWithCorners (n := âŠ¤)
      (isClosed_closure (s := Set.range (hom f))) (isClosed_singleton (x := x)) (by simpa using hx)
    have hg' := hf'.left_cancellation (Z := â„') (ofHom g) (ofHom (.const 0))
      (by ext y; exact congrFun (Set.eqOn_range.1 <| hg.1.mono subset_closure) y)
    exact zero_ne_one ((CategoryTheory.congr_fun hg' x).symm.trans <| hg.2.1 (Set.mem_singleton _))
  Â· apply ConcreteCategory.coe_ext
    rw [â† Set.eqOn_univ, â† hf.closure_range]
    refine Set.EqOn.closure ?_ (map_continuous _) (map_continuous _)
    exact Set.eqOn_range.2 <| congrArg (fun (f : M âŸ¶ _) â†¦ â‡‘(hom f)) hg

instance {X : Type*} [TopologicalSpace X] [LocallyCompactSpace X] (u : Opens X) :
    LocallyCompactSpace u :=
  u.2.locallyCompactSpace

instance {M : FinDimMfld â„ âˆ} : SecondCountableTopology M := by
  have := M.1.modelWithCorners.toHomeomorphTarget.secondCountableTopology
  exact ChartedSpace.secondCountable_of_sigmaCompact M.1.model M

instance {M : FinDimMfld â„ âˆ} : LocallyCompactSpace M := by
  have := M.1.modelWithCorners.toHomeomorphTarget.locallyCompactSpace_iff.2 <|
    M.1.modelWithCorners.range_eq_target â–¸ M.1.modelWithCorners.isClosed_range.locallyCompactSpace
  exact ChartedSpace.locallyCompactSpace M.1.model M

noncomputable abbrev mkOfOpen {M : FinDimMfld â„ âˆ} (u : Opens M) :
    FinDimMfld â„ âˆ :=
  .mk' u M.1.modelWithCorners

end FinDimMfld
