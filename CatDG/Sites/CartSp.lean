import CatDG.Cohesive.CohesiveSite
import CatDG.Diffeology.Algebra.Module
import CatDG.ForMathlib.ConcreteSite
import Mathlib.CategoryTheory.Sites.Canonical

/-!
# The site `CartSp` of cartesian spaces ‚Ñù‚Åø and smooth maps

In this file we define the category `CartSp` of cartesian spaces ‚Ñù‚Åø and smooth maps between them,
and equip it with the open cover coverage. This site is of relevance because it is the simplest of
several sites on which concrete sheaves correspond exactly to diffeological spaces.

See https://ncatlab.org/nlab/show/CartSp.

Note that with the current implementation, this could not be used to *define*
diffeological spaces - it already uses diffeology in the definition of
`CartSp.openCoverCoverage`. The reason is that smooth embeddings are apparently not yet
implemented in mathlib, so diffeological inductions are used instead.

## Main definitions / results:
* `CartSp`: the category of euclidean spaces and smooth maps between them
* `CartSp.openCoverCoverage`: the coverage given by jointly surjective open inductions
* `CartSp` has all finite products
* `CartSp` is a concrete, cohesive and subcanonical site

## TODO
* Switch from `HasForget` to the new `ConcreteCategory` design
* Use `Presieve.IsJointlySurjective` more (currently runs into problems regarding which `FunLike`
  instances are used)
* Generalise `CartSp` to take a smoothness parameter in `‚Ñï‚àû`
* General results about concrete sites
-/

universe u

open CategoryTheory Limits Sheaf TopologicalSpace Set

def CartSp := ‚Ñï

namespace CartSp

instance : CoeSort CartSp Type where
  coe n := EuclideanSpace ‚Ñù (Fin n)

instance (n : ‚Ñï) : OfNat CartSp n where
  ofNat := n

noncomputable instance : SmallCategory CartSp where
  Hom := fun n m ‚Ü¶ DSmoothMap n m
  id := fun n ‚Ü¶ DSmoothMap.id _
  comp := fun f g ‚Ü¶ g.comp f

instance : HasForget CartSp where
  forget := { obj := fun n ‚Ü¶ n, map := fun f ‚Ü¶ f.1 }
  forget_faithful := { map_injective := fun {_ _} ‚Ü¶ DSmoothMap.coe_injective }

instance instFunLike (n m : CartSp) : FunLike (n ‚ü∂ m) n m where
  coe f := DFunLike.coe (F := DSmoothMap _ _) f
  coe_injective' := DFunLike.coe_injective (F := DSmoothMap _ _)

@[simp]
theorem id_app (n : CartSp) (x : n) : (ùüô n : n ‚ü∂ n) x = x := rfl

@[simp]
theorem comp_app {n m k : CartSp} (f : n ‚ü∂ m) (g : m ‚ü∂ k) (x : n) :
    (f ‚â´ g : n ‚Üí k) x = g (f x) := rfl

/-- The open cover coverage on `CartSp`, consisting of all coverings by open smooth embeddings.
Since mathlib apparently doesn't have smooth embeddings yet, diffeological inductions are
used instead. -/
def openCoverCoverage : Coverage CartSp where
  coverings n := {s | (‚àÄ (m : _) (f : m ‚ü∂ n), s f ‚Üí IsOpenInduction f) ‚àß
    ‚ãÉ (m : CartSp) (f ‚àà s (Y := m)), range f.1 = univ}
  pullback n m g s hs := by
    use fun k ‚Ü¶ {f | (‚àÉ (k : _) (f' : k ‚ü∂ n), s f' ‚àß range (g.1 ‚àò f.1) ‚äÜ range f'.1)
      ‚àß IsOpenInduction f}
    refine ‚ü®‚ü®fun k f hf ‚Ü¶ hf.2, ?_‚ü©, ?_‚ü©
    ¬∑ refine iUnion_eq_univ_iff.2 fun x ‚Ü¶ ?_
      let ‚ü®k,hk‚ü© := iUnion_eq_univ_iff.1 hs.2 (g x)
      let ‚ü®f,hf,hgx‚ü© := mem_iUnion‚ÇÇ.1 hk
      refine ‚ü®m, mem_iUnion‚ÇÇ.2 ?_‚ü©
      let ‚ü®Œµ, hŒµ, hxŒµ‚ü© := Metric.isOpen_iff.1
        ((hs.1 k f hf).isOpen_range.preimage g.2.continuous) x hgx
      let e := (DDiffeomorph.univBall x hŒµ)
      use ‚ü®_, dsmooth_subtype_val.comp e.dsmooth‚ü©
      refine ‚ü®‚ü®?_, ?_‚ü©, ?_‚ü©
      ¬∑ refine ‚ü®k, f, hf, _root_.subset_trans ?_ (image_subset_iff.2 hxŒµ)‚ü©
        simp_rw [range_comp]; apply image_mono; simpa using subset_rfl
      ¬∑ refine (IsOpen.isOpenInduction_subtype_val' Metric.isOpen_ball).comp e.isOpenInduction
      ¬∑ change x ‚àà range (Subtype.val ‚àò e.toEquiv)
        rw [e.toEquiv.surjective.range_comp]; simp [hŒµ]
    ¬∑ intro k f ‚ü®‚ü®k',f',hf'‚ü©,_‚ü©; use k'
      let f'' := (DDiffeomorph.ofIsInduction (hs.1 k' f' hf'.1).1)
      use ‚ü®_,(f''.dsmooth_invFun.comp <|
        (f ‚â´ g).2.subtype_mk (fun x ‚Ü¶ hf'.2 (mem_range_self x)))‚ü©
      refine ‚ü®f', hf'.1, ?_‚ü©; ext x; change f'.1 (f''.invFun _) = _
      simp_rw [show f'.1 = Subtype.val ‚àò f'' by rfl]
      dsimp; exact congrArg Subtype.val <| f''.apply_symm_apply _

/-- The open cover grothendieck topology on `CartSp`. -/
def openCoverTopology : GrothendieckTopology CartSp :=
  openCoverCoverage.toGrothendieck

/-- A sieve belongs to `CartSp.openCoverTopology` iff it contains a presieve from
`CartSp.openCoverCoverage`. -/
lemma openCoverTopology.mem_sieves_iff {n : CartSp} {s : Sieve n} :
    s ‚àà openCoverTopology n ‚Üî ‚àÉ r, r ‚â§ s.arrows ‚àß r ‚àà openCoverCoverage n := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun ‚ü®r, hr‚ü© ‚Ü¶ Coverage.mem_toGrothendieck_sieves_of_superset _ hr.1 hr.2‚ü©
  induction h with
  | of n s hs =>
    exact ‚ü®s, Sieve.le_generate s, hs‚ü©
  | top n =>
    refine ‚ü®fun k f ‚Ü¶ IsOpenInduction f, le_top, fun k f hf ‚Ü¶ hf, ?_‚ü©
    exact univ_subset_iff.1 <| subset_iUnion_of_subset n <|
        subset_iUnion‚ÇÇ_of_subset (ùüô n) isOpenInduction_id (range_id.symm.subset)
  | transitive n s r _ _ hs hr =>
    let ‚ü®s', hs'‚ü© := hs
    refine ‚ü®fun k f ‚Ü¶ r f ‚àß IsOpenInduction f, fun _ _ h ‚Ü¶ h.1, fun _ _ h ‚Ü¶ h.2, ?_‚ü©
    rw [‚Üê univ_subset_iff, ‚Üê hs'.2.2]
    refine iUnion_subset fun m ‚Ü¶ iUnion‚ÇÇ_subset fun f hf ‚Ü¶ ?_
    let ‚ü®r', hr'‚ü© := hr (hs'.1 _ hf)
    simp_rw [‚Üê image_univ, ‚Üê hr'.2.2, image_iUnion]
    refine iUnion_subset fun k ‚Ü¶ iUnion‚ÇÇ_subset fun g hg ‚Ü¶ ?_
    refine subset_iUnion_of_subset k <| subset_iUnion‚ÇÇ_of_subset (g ‚â´ f) ‚ü®?_, ?_‚ü© ?_
    ¬∑ exact hr'.1 _ hg
    ¬∑ exact (hs'.2.1 _ _ hf).comp (hr'.2.1 _ _ hg)
    ¬∑ rw [‚Üê range_comp, image_univ]; rfl

/- A sieve belongs to `CartSp.openCoverTopology` iff the open inductions in it are jointly
surjective. -/
lemma openCoverTopology.mem_sieves_iff' {n : CartSp} {s : Sieve n} :
    s ‚àà openCoverTopology n ‚Üî
    ‚ãÉ (m) (f : m ‚ü∂ n) (_ : s f ‚àß IsOpenInduction f), range f = univ := by
  refine mem_sieves_iff.trans ‚ü®fun ‚ü®r, hr‚ü© ‚Ü¶ ?_, fun h ‚Ü¶ ?_‚ü©
  ¬∑ rw [‚Üê univ_subset_iff, ‚Üê hr.2.2]
    exact iUnion_subset fun m ‚Ü¶ iUnion‚ÇÇ_subset fun f hf ‚Ü¶ subset_iUnion_of_subset m <|
      subset_iUnion‚ÇÇ_of_subset f ‚ü®hr.1 _ hf, hr.2.1 m f hf‚ü© subset_rfl
  ¬∑ exact ‚ü®fun m f ‚Ü¶ s f ‚àß IsOpenInduction f, fun _ _ h ‚Ü¶ h.1, fun _ _ h ‚Ü¶ h.2, h‚ü©

/-- The `0`-dimensional cartesian space is terminal in `CartSp`. -/
noncomputable def isTerminal0 : IsTerminal (0 : CartSp) where
  lift s := DSmoothMap.const _ 0
  uniq c f h := by ext x; exact Subsingleton.elim (Œ± := EuclideanSpace ‚Ñù (Fin 0)) (f x) 0

/-- The first projection realising `EuclideanSpace ‚Ñù (Fin (n + m))` as the product of
`EuclideanSpace ‚Ñù n` and `EuclideanSpace ‚Ñù m`. -/
noncomputable abbrev prodFst {n m : CartSp} :
    @Quiver.Hom CartSp _ (@HAdd.hAdd ‚Ñï ‚Ñï _ _ n m) n :=
  DSmoothMap.fst.comp EuclideanSpace.finAddEquivProd.toDDiffeomorph.toDSmoothMap

/-- The second projection realising `EuclideanSpace ‚Ñù (Fin (n + m))` as the product of
`EuclideanSpace ‚Ñù n` and `EuclideanSpace ‚Ñù m`. -/
noncomputable abbrev prodSnd {n m : CartSp} :
    @Quiver.Hom CartSp _ (@HAdd.hAdd ‚Ñï ‚Ñï _ _ n m) m :=
  DSmoothMap.snd.comp EuclideanSpace.finAddEquivProd.toDDiffeomorph.toDSmoothMap

/-- The explicit binary fan of `EuclideanSpace ‚Ñù n` and `EuclideanSpace ‚Ñù m` given by
`EuclideanSpace ‚Ñù (Fin (n + m))`. -/
noncomputable def prodBinaryFan (n m : CartSp) : BinaryFan n m :=
  BinaryFan.mk prodFst prodSnd

/-- The constructed binary fan is indeed a limit. -/
noncomputable def prodBinaryFanIsLimit (n m : CartSp) : IsLimit (prodBinaryFan n m) where
  lift c := EuclideanSpace.finAddEquivProd.toDDiffeomorph.symm.toDSmoothMap.comp <|
    DSmoothMap.prodMk (BinaryFan.fst c) (BinaryFan.snd c)
  fac := by
    rintro c (_ | _) <;> dsimp [prodBinaryFan, prodFst]
    all_goals ext (x : EuclideanSpace _ _)
    -- TODO: figure out how to better deal with simp-breaking situations like this
    all_goals rw [CategoryTheory.comp_apply]
    ¬∑ exact congrArg Prod.fst (EuclideanSpace.finAddEquivProd.toDDiffeomorph.apply_symm_apply _)
    ¬∑ exact congrArg Prod.snd (EuclideanSpace.finAddEquivProd.toDDiffeomorph.apply_symm_apply _)
  uniq c f h := by
    ext x
    change _ = EuclideanSpace.finAddEquivProd.toDDiffeomorph.toEquiv.symm _
    rw [Equiv.eq_symm_apply]
    refine Prod.ext ?_ ?_
    ¬∑ simpa using congrFun (congrArg DSmoothMap.toFun (h { as := WalkingPair.left })) x
    ¬∑ simpa using congrFun (congrArg DSmoothMap.toFun (h { as := WalkingPair.right })) x

instance : HasFiniteProducts CartSp := by
  refine @hasFiniteProducts_of_has_binary_and_terminal _ _ ?_ isTerminal0.hasTerminal
  exact @hasBinaryProducts_of_hasLimit_pair _ _ ‚ü®‚ü®_, prodBinaryFanIsLimit _ _‚ü©‚ü©

-- TODO: figure out how to get this from more general instances
noncomputable instance : Unique (‚ä§_ CartSp) := by
  have : Unique ((forget CartSp).obj 0) := inferInstanceAs (Unique (Eucl 0))
  exact ((forget _).mapIso (terminalIsTerminal.uniqueUpToIso isTerminal0)).toEquiv.unique

/-- `CartSp` is a locally connected site, roughly meaning that each of its objects is connected.
Note that this is different from `EuclOp`, which also contains disconnected open sets and thus isn't
locally connected. -/
instance : openCoverTopology.IsLocallyConnectedSite where
  isConnected_of_mem {n} s hs := by
    simp_rw [openCoverTopology.mem_sieves_iff', eq_univ_iff_forall, mem_iUnion] at hs
    have hs' : ‚àÄ f : ‚ä§_ _ ‚ü∂ n, s.arrows f := fun f ‚Ü¶ by
      let ‚ü®m, g, hg, x, hx‚ü© := hs (f 0)
      convert s.downward_closed (Z := ‚ä§_ _) hg.1 (DSmoothMap.const _ x)
      ext _; exact (congrArg _ (Subsingleton.allEq (Œ± := ‚ä§_ CartSp) _ _)).trans hx.symm
    have _ : Nonempty s.arrows.category := ‚ü®.mk (Y := ‚ä§_ _) (DSmoothMap.const _ 0), hs' _‚ü©
    refine .of_constant_of_preserves_morphisms fun {Œ±} F hF ‚Ü¶ ?_
    let F' : n ‚Üí Œ± := fun x ‚Ü¶ F ‚ü®.mk (DSmoothMap.const _ x), hs' _‚ü©
    have hF' : IsLocallyConstant F' := by
      refine (IsLocallyConstant.iff_exists_open _).2 fun x ‚Ü¶ ?_
      let ‚ü®m, f, hf, y, hy‚ü© := hs x
      refine ‚ü®range f, hf.2.isOpen_range, ‚ü®y, hy‚ü©, fun x' ‚ü®y', hy'‚ü© ‚Ü¶ ?_‚ü©
      rw [‚Üê hy, ‚Üê hy']
      exact (@hF ‚ü®.mk (DSmoothMap.const _ (f y')), hs' _‚ü© ‚ü®.mk f, hf.1‚ü©
        (Over.homMk (DSmoothMap.const _ y'))).trans
        (@hF ‚ü®.mk (DSmoothMap.const _ (f y)), hs' _‚ü© ‚ü®.mk f, hf.1‚ü©
          (Over.homMk (DSmoothMap.const _ y))).symm
    exact fun f g ‚Ü¶ (@hF ‚ü®.mk (DSmoothMap.const _ (f.1.hom 0)), hs' _‚ü© ‚ü®.mk f.1.hom, f.2‚ü©
      (Over.homMk (DSmoothMap.const _ 0))).symm.trans <|
      (IsLocallyConstant.iff_is_const.1 hF' _ _).trans <|
      @hF ‚ü®.mk (DSmoothMap.const _ (g.1.hom 0)), hs' _‚ü© ‚ü®.mk g.1.hom, g.2‚ü©
        (Over.homMk (DSmoothMap.const _ 0))

/-- `CartSp` is a cohesive site (i.e. in particular locally connected and local, but with a few more
properties). From this it follows that the sheaves on it form a cohesive topos.

Note that `EuclOp` (defined in another file) is *not* a cohesive site, as it isn't locally
connected. Sheaves on it form a cohesive topos too nonetheless, simply because the sheaf topoi on
`EuclOp` and `CartSp` are equivalent. -/
instance : openCoverTopology.IsCohesiveSite where
  nonempty_fromTerminal := ‚ü®DSmoothMap.const _ 0‚ü©

/-- `CartSp` is a concrete site, in that it is concrete with elements corresponding to morphisms
from the terminal object and carries a topology consisting entirely of jointly surjective sieves. -/
noncomputable instance : openCoverTopology.IsConcreteSite where
  forgetNatIsoCoyoneda := NatIso.ofComponents fun n ‚Ü¶
    (DSmoothMap.equivFnOfUnique (Y := Eucl n)).toIso.symm
  forgetNatIsoCoyoneda_apply := rfl
  isJointlySurjective_of_mem hs := by
    rw [openCoverTopology.mem_sieves_iff] at hs
    obtain ‚ü®r, hr‚ü© := hs
    exact .mono hr.1 <| Presieve.isJointlySurjective_iff_iUnion_range_eq_univ.2 hr.2.2

open GrothendieckTopology.IsConcreteSite in
/-- `CartSp` is a subcanonical site, i.e. all representable presheaves on it are sheaves. -/
instance : openCoverTopology.Subcanonical := by
  refine .of_isSheaf_yoneda_obj _ fun n m s hs ‚Ü¶
    (isSeparated_yoneda_obj _ n s hs).isSheafFor fun f hf ‚Ü¶ ?_
  let hs' := hs; simp_rw [openCoverTopology.mem_sieves_iff', eq_univ_iff_forall, mem_iUnion] at hs'
  refine ‚ü®‚ü®?_, ?_‚ü©, ?_‚ü©
  ¬∑ intro x
    exact (show ‚ä§_ CartSp ‚ü∂ n from f _ <| from_terminal_mem_of_mem _ hs (.const _ x)) default
  ¬∑ refine dsmooth_iff_locally_dsmooth.2 fun x ‚Ü¶ ?_
    let ‚ü®k, g, hg, hx‚ü© := hs' x
    refine ‚ü®_, hg.2.isOpen_range, hx, ?_‚ü©
    rw [‚Üê hg.2.dsmooth_comp_iff_dsmooth_restrict]
    convert (f g hg.1).dsmooth; ext1 x'
    specialize hf (ùüô (‚ä§_ _)) (Y‚ÇÇ := k) (DSmoothMap.const _ x')
      (from_terminal_mem_of_mem _ hs (.const _ (g x'))) hg.1 rfl
    exact congrFun (congrArg DSmoothMap.toFun hf) (default : ‚ä§_ CartSp)
  ¬∑ intro k g hg; dsimp; ext1 x
    specialize hf (ùüô (‚ä§_ _)) (Y‚ÇÇ := k) (DSmoothMap.const _ x)
      (from_terminal_mem_of_mem _ hs (.const _ (g x))) hg rfl
    exact congrFun (congrArg DSmoothMap.toFun hf) (default : ‚ä§_ CartSp)

end CartSp
