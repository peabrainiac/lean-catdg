import Mathlib.Algebra.Category.AlgebraCat.Basic
import Mathlib.CategoryTheory.Sites.Coverage
import Mathlib.CategoryTheory.Sites.DenseSubSite.Basic
import Orbifolds.Diffeology.Algebra.DSmoothMap

/-!
# CartSp and EuclOp

This file defines the sites `CartSp` and `EuclOp` of cartesian spaces resp. open subsets of
cartesian spaces and smooth maps, both with the good open cover coverage. These
sites are of relevance because concrete sheaves on them correspond to diffeological spaces,
although sheaves on them are studied not in this file but in `Orbifolds.Diffeology.SmoothSp`.

See https://ncatlab.org/nlab/show/CartSp.

Note that with the current implementation, this could not be used to *define*
diffeological spaces - it already uses diffeology in the definition of
`CartSp.openCoverCoverage`. The reason is that smooth embeddings are apparently not yet
implemented in mathlib, so diffeological inductions are used instead.

Main definitions / results:
* `CartSp`: the category of euclidean spaces and smooth maps between them
* `CartSp.openCoverCoverage`: the coverage given by jointly surjective open inductions
* `EuclOp`: the category of open subsets of euclidean spaces and smooth maps between them
* `EuclOp.openCoverCoverage`: the coverage given by jointly surjective open inductions
* `CartSp.toEuclOp`: the fully faithful embedding of `CartSp` into `EuclOp`

## TODO
* Switch from `HasForget` to the new `ConcreteCategory` design
* `CartSp.toEuclOp` makes `CartSp` a dense sub-site of `EuclOp`
* Generalise `CartSp` to take a smoothness parameter in `‚Ñï‚àû`
* Generalise `EuclOp` to take a smoothness parameter in `WithTop ‚Ñï‚àû`
* General results about concrete sites
-/

universe u

open CategoryTheory Sheaf TopologicalSpace

def CartSp := ‚Ñï

instance : CoeSort CartSp Type where
  coe n := EuclideanSpace ‚Ñù (Fin n)

instance (n : ‚Ñï) : OfNat CartSp n where
  ofNat := n

instance : SmallCategory CartSp where
  Hom := fun n m ‚Ü¶ DSmoothMap n m
  id := fun n ‚Ü¶ DSmoothMap.id
  comp := fun f g ‚Ü¶ g.comp f

instance : HasForget CartSp where
  forget := { obj := fun n ‚Ü¶ n, map := fun f ‚Ü¶ f.1 }
  forget_faithful := { map_injective := fun {_ _} ‚Ü¶ DSmoothMap.coe_injective }

instance CartSp.instFunLike (n m : CartSp) : FunLike (n ‚ü∂ m) n m where
  coe := Subtype.val
  coe_injective' := Subtype.coe_injective

@[simp]
theorem CartSp.id_app (n : CartSp) (x : n) : (ùüô n : n ‚ü∂ n) x = x := rfl

@[simp]
theorem CartSp.comp_app {n m k : CartSp} (f : n ‚ü∂ m) (g : m ‚ü∂ k) (x : n) :
    (f ‚â´ g : n ‚Üí k) x = g (f x) := rfl

/-- The open cover coverage on `CartSp`, consisting of all coverings by open smooth embeddings.
  Since mathlib apparently doesn't have smooth embeddings yet, diffeological inductions are
  used instead. -/
def CartSp.openCoverCoverage : Coverage CartSp where
  covering n := {s | (‚àÄ (m : _) (f : m ‚ü∂ n), s f ‚Üí Induction f.1 ‚àß IsOpenMap f.1) ‚àß
    ‚ãÉ (m : CartSp) (f ‚àà s (Y := m)), Set.range f.1 = Set.univ}
  pullback n m g s hs := by
    use fun k ‚Ü¶ {f | (‚àÉ (k : _) (f' : k ‚ü∂ n), s f' ‚àß Set.range (g.1 ‚àò f.1) ‚äÜ Set.range f'.1)
      ‚àß Induction f.1 ‚àß IsOpenMap f.1}
    refine ‚ü®‚ü®fun k f hf ‚Ü¶ hf.2, ?_‚ü©, ?_‚ü©
    ¬∑ refine Set.iUnion_eq_univ_iff.2 fun x ‚Ü¶ ?_
      let ‚ü®k,hk‚ü© := Set.iUnion_eq_univ_iff.1 hs.2 (g x)
      let ‚ü®f,hf,hgx‚ü© := Set.mem_iUnion‚ÇÇ.1 hk
      refine ‚ü®m, Set.mem_iUnion‚ÇÇ.2 ?_‚ü©
      let ‚ü®Œµ, hŒµ, hxŒµ‚ü© := Metric.isOpen_iff.1
        ((hs.1 k f hf).2.isOpen_range.preimage g.2.continuous) x hgx
      let e := (DDiffeomorph.univBall x hŒµ)
      use ‚ü®_, dsmooth_subtype_val.comp e.dsmooth‚ü©
      refine ‚ü®‚ü®?_, ?_‚ü©, ?_‚ü©
      ¬∑ refine ‚ü®k, f, hf, _root_.subset_trans ?_ (Set.image_subset_iff.2 hxŒµ)‚ü©
        simp_rw [Set.range_comp]; apply Set.image_mono; simpa using subset_rfl
      ¬∑ refine ‚ü®induction_subtype_val.comp e.induction, ?_‚ü©
        have := (Metric.isOpen_ball  (x := x) (Œµ := Œµ)).dTopCompatible
        exact (Metric.isOpen_ball).isOpenMap_subtype_val.comp e.toHomeomorph'.isOpenMap
      ¬∑ change x ‚àà Set.range (Subtype.val ‚àò e.toEquiv)
        rw [e.toEquiv.surjective.range_comp]; simp [hŒµ]
    ¬∑ intro k f ‚ü®‚ü®k',f',hf'‚ü©,_‚ü©; use k'
      let f'' := (DDiffeomorph.ofInduction (hs.1 k' f' hf'.1).1)
      use ‚ü®_,(f''.dsmooth_invFun.comp <|
        (f ‚â´ g).2.subtype_mk (fun x ‚Ü¶ hf'.2 (Set.mem_range_self x)))‚ü©
      refine ‚ü®f', hf'.1, ?_‚ü©; ext x; change f'.1 (f''.invFun _) = _
      simp_rw [show f'.1 = Subtype.val ‚àò f'' by rfl]
      dsimp; rw [DDiffeomorph.apply_symm_apply,CategoryTheory.comp_apply]; rfl

/-- The open cover grothendieck topology on `CartSp`. -/
def CartSp.openCoverTopology : GrothendieckTopology CartSp :=
  openCoverCoverage.toGrothendieck

def EuclOp := (n : ‚Ñï) √ó Opens (EuclideanSpace ‚Ñù (Fin n))

instance : CoeSort EuclOp Type where
  coe u := u.2

instance : SmallCategory EuclOp where
  Hom := fun u v ‚Ü¶ DSmoothMap u v
  id := fun n ‚Ü¶ DSmoothMap.id
  comp := fun f g ‚Ü¶ g.comp f

instance : HasForget EuclOp where
  forget := { obj := fun u ‚Ü¶ u, map := fun f ‚Ü¶ f.1 }
  forget_faithful := { map_injective := fun {_ _} ‚Ü¶ DSmoothMap.coe_injective }

instance EuclOp.instFunLike (u v : EuclOp) : FunLike (u ‚ü∂ v) u v where
  coe := Subtype.val
  coe_injective' := Subtype.coe_injective

@[simp]
theorem EuclOp.id_app (u : EuclOp) (x : u) : (ùüô u : u ‚ü∂ u) x = x := rfl

@[simp]
theorem EuclOp.comp_app {u v w : EuclOp} (f : u ‚ü∂ v) (g : v ‚ü∂ w) (x : u) :
    (f ‚â´ g : u ‚Üí w) x = g (f x) := rfl

/-- The open cover coverage on `EuclOp`, consisting of all coverings by open smooth embeddings.
  Since mathlib apparently doesn't have smooth embeddings yet, diffeological inductions are
  used instead. -/
def EuclOp.openCoverCoverage : Coverage EuclOp where
  covering u := {s | (‚àÄ (v : _) (f : v ‚ü∂ u), s f ‚Üí Induction f.1 ‚àß IsOpenMap f.1) ‚àß
    ‚ãÉ (v : EuclOp) (f ‚àà s (Y := v)), Set.range f.1 = Set.univ}
  pullback u v g s hs := by
    use fun k ‚Ü¶ {f | (‚àÉ (k : _) (f' : k ‚ü∂ u), s f' ‚àß Set.range (g.1 ‚àò f.1) ‚äÜ Set.range f'.1)
      ‚àß Induction f.1 ‚àß IsOpenMap f.1}
    refine ‚ü®‚ü®fun k f hf ‚Ü¶ hf.2, ?_‚ü©, ?_‚ü©
    ¬∑ refine Set.iUnion_eq_univ_iff.2 fun x ‚Ü¶ ?_
      let ‚ü®w,hw‚ü© := Set.iUnion_eq_univ_iff.1 hs.2 (g x)
      let ‚ü®f,hf,hgx‚ü© := Set.mem_iUnion‚ÇÇ.1 hw
      have h := v.2.2.isOpenMap_subtype_val _
        ((hs.1 _ _ hf).2.isOpen_range.preimage g.2.continuous')
      use ‚ü®_, _, h‚ü©
      refine Set.mem_iUnion‚ÇÇ.2 ‚ü®‚ü®_, dsmooth_inclusion (Subtype.coe_image_subset _ _)‚ü©, ?_‚ü©
      refine ‚ü®‚ü®‚ü®w, f, hf, ?_‚ü©, ?_, ?_‚ü©, ?_‚ü©
      ¬∑ simp only [Opens.carrier_eq_coe, SetLike.coe_sort_coe]
        rw [Set.range_comp, Set.range_inclusion]
        convert Set.image_preimage_subset _ _; ext x
        rw [Set.mem_setOf_eq, Subtype.val_injective.mem_set_image]
      ¬∑ exact induction_inclusion <| Subtype.coe_image_subset _ _
      ¬∑ exact h.isOpenMap_inclusion <| Subtype.coe_image_subset _ _
      ¬∑ dsimp; rw [Set.range_inclusion]; exact ‚ü®_, hgx, rfl‚ü©
    ¬∑ intro k f ‚ü®‚ü®k',f',hf'‚ü©,_‚ü©; use k'
      let f'' := (DDiffeomorph.ofInduction (hs.1 k' f' hf'.1).1)
      use ‚ü®_,(f''.dsmooth_invFun.comp <|
        (f ‚â´ g).2.subtype_mk (fun x ‚Ü¶ hf'.2 (Set.mem_range_self x)))‚ü©
      refine ‚ü®f', hf'.1, ?_‚ü©; ext x; change f'.1 (f''.invFun _) = _
      simp_rw [show f'.1 = Subtype.val ‚àò f'' by rfl]
      dsimp; rw [DDiffeomorph.apply_symm_apply,CategoryTheory.comp_apply]; rfl

/-- The open cover grothendieck topology on `EuclOp`. -/
def EuclOp.openCoverTopology : GrothendieckTopology EuclOp :=
  openCoverCoverage.toGrothendieck

/-- The embedding of `CartSp` into `EuclOp`. -/
noncomputable def CartSp.toEuclOp : CartSp ‚•§ EuclOp where
  obj n := ‚ü®n, ‚ä§‚ü©
  map f := ‚ü®_, f.2.restrict (Set.mapsTo_univ f Set.univ)‚ü©

/-- Open subsets of cartesian spaces can be covered with cartesian spaces. -/
instance : CartSp.toEuclOp.IsCoverDense EuclOp.openCoverTopology := by
  constructor; intro u
  refine EuclOp.openCoverCoverage.mem_toGrothendieck_sieves_of_superset (R := ?_) ?_ ?_
  ¬∑ exact fun {v} f ‚Ü¶ v.2.1 = Set.univ ‚àß Induction f.1 ‚àß IsOpenMap f.1
  ¬∑ intro v f hf
    refine ‚ü®‚ü®v.1, ‚ü®_, dsmooth_id.restrict (Set.mapsTo_univ _ _)‚ü©, ?_, ?_‚ü©‚ü©
    ¬∑ let e : CartSp.toEuclOp.obj v.1 ‚ü∂ v :=
        ‚ü®_, dsmooth_id.restrict (by convert Set.mapsTo_univ _ _; exact hf.1)‚ü©
      exact e ‚â´ f
    ¬∑ ext x; rfl
  ¬∑ refine ‚ü®fun v f hf ‚Ü¶ hf.2, Set.iUnion_eq_univ_iff.2 fun x ‚Ü¶ ?_‚ü©
    use ‚ü®u.1, ‚ä§‚ü©; apply Set.mem_iUnion‚ÇÇ.2
    let ‚ü®Œµ, hŒµ, hxŒµ‚ü© := Metric.isOpen_iff.1 u.2.2 x.1 x.2
    let e := (DDiffeomorph.Set.univ _).trans (DDiffeomorph.univBall x.1 hŒµ)
    use ‚ü®_, (dsmooth_inclusion hxŒµ).comp e.dsmooth‚ü©
    refine ‚ü®‚ü®rfl, ?_, ?_‚ü©, ?_‚ü©
    ¬∑ exact (induction_inclusion hxŒµ).comp e.induction
    ¬∑ have := (@isOpen_univ (EuclideanSpace ‚Ñù (Fin u.1)) _).dTopCompatible
      have h : IsOpen (Metric.ball x.1 Œµ) := Metric.isOpen_ball
      have := h.dTopCompatible
      exact (h.isOpenMap_inclusion hxŒµ).comp e.toHomeomorph'.isOpenMap
    ¬∑ rw [Set.range_comp, e.surjective.range_eq, Set.image_univ]
      use ‚ü®x.1, Metric.mem_ball_self hŒµ‚ü©; rfl

instance CartSp.toEuclOp_fullyFaithful : CartSp.toEuclOp.FullyFaithful where
  preimage {n m} f := by
    exact ‚ü®_, (dsmooth_subtype_val.comp f.2).comp (dsmooth_id.subtype_mk (Set.mem_univ))‚ü©

instance : CartSp.toEuclOp.Full := CartSp.toEuclOp_fullyFaithful.full

instance : CartSp.toEuclOp.Faithful := CartSp.toEuclOp_fullyFaithful.faithful

instance : CartSp.toEuclOp.IsDenseSubsite
    CartSp.openCoverTopology EuclOp.openCoverTopology where
  functorPushforward_mem_iff {n} s := by
    unfold EuclOp.openCoverTopology CartSp.openCoverTopology
      EuclOp.openCoverCoverage CartSp.openCoverCoverage
    --rw [Coverage.toGrothendieck_eq_sInf]
    sorry

/-!
### Embeddings into other categories
TODO: split this off into some other file, to reduce the imports of this file
-/

section Embeddings

example {n : ‚Ñï} : DSmoothSMul ‚Ñù (Eucl n) := inferInstance

/-- The embedding of `CartSp` into the opposite category of `‚Ñù`-algebras, sending each space `X`
to the algebra of smooth maps `X ‚Üí ‚Ñù`.
TODO: change this to the category of commutative algebras once #23601 is merged into mathlib -/
@[simps!]
noncomputable def CartSp.toAlgebraCatOp : CartSp ‚•§ (AlgebraCat ‚Ñù)·µí·µñ where
  obj X := .op (.of ‚Ñù (DSmoothMap X ‚Ñù))
  map {n m} f := .op <| AlgebraCat.ofHom f.compRightAlgHom

noncomputable def CartSp.toAlgebraCatOpFullyFaithful : CartSp.toAlgebraCatOp.FullyFaithful where
  preimage {n m} f := by
    let f' (k : Fin m) : DSmoothMap _ _ := f.unop ‚ü®_, (EuclideanSpace.proj (ùïú := ‚Ñù) k).dsmooth‚ü©
    exact (‚àë k, f' k ‚Ä¢ DSmoothMap.const (X := Eucl n) (EuclideanSpace.single k (1 : ‚Ñù)):)
    /-exact ‚ü®_, dsmooth_finset_sum Finset.univ fun k _ ‚Ü¶
      (f.unop ‚ü®_, (EuclideanSpace.proj k).dsmooth‚ü©).dsmooth.smul <|
        dsmooth_const (y := EuclideanSpace.single k (1 : ‚Ñù))‚ü©-/
  map_preimage {n m} f := by
    refine Quiver.Hom.unop_inj ?_
    ext1; ext1 (g : DSmoothMap _ _)
    dsimp [DSmoothMap.compRightAlgHom, DSmoothMap.compRightRingHom]
    ext x
    --have := (ConcreteCategory.hom f.unop:)
    --have := DSmoothMap.compRightLinearMap' g (R := ‚Ñù)
    -- TODO: finish this. might require something like Hadamard's lemma?
    sorry
  preimage_map f := by
    refine DSmoothMap.ext fun x ‚Ü¶ ?_
    simpa using (EuclideanSpace.basisFun _ ‚Ñù).sum_repr (f x)

instance : CartSp.toAlgebraCatOp.Full := CartSp.toAlgebraCatOpFullyFaithful.full

instance : CartSp.toAlgebraCatOp.Faithful := CartSp.toAlgebraCatOpFullyFaithful.faithful

end Embeddings
