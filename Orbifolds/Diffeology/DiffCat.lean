import Mathlib.CategoryTheory.ConcreteCategory.BundledHom
import Mathlib.CategoryTheory.Adjunction.Basic
import Mathlib.CategoryTheory.Limits.Types
import Mathlib.CategoryTheory.Limits.Preserves.Basic
import Mathlib.CategoryTheory.Closed.Cartesian
import Mathlib.Topology.Category.TopCat.Basic
import Orbifolds.Diffeology.DDiffeomorph

/-!
# Category of diffeological spaces
The category of diffeological spaces and smooth maps.
Adapted from `Mathlib.Topology.Category.TopCat.Basic`.

Main definitions / results:
* `DiffCat`: the category of diffeological spaces and smooth maps.
* `forget DiffCat`: the forgetful functor `DiffCat ‚•§ Type`,
  provided through a `ConcreteCategory`-instance on `DiffCat`.
* `DiffCat.discrete`, `DiffCat.indiscrete`: the functors `Type ‚•§ DiffCat` giving each type the
  discrete/indiscrete diffeology.
* `DiffCat.dTop`: the functor `DiffCat ‚•§ TopCat` giving each space its D-topology.
* `DiffCat.discreteForgetAdj`, `DiffCat.forgetIndiscreteAdj`: the adjunctions
  `discrete ‚ä£ forget ‚ä£ indiscrete`.
* `DiffCat.hasLimits`, `DiffCat.hasColimits`: `DiffCat` is complete and cocomplete.
* `DiffCat.forgetPreservesLimits`, `DiffCat.forgetPreservesColimits`: the forgetful functor
  `DiffCat ‚•§ Type` preserves limits and colimits.
-/

open CategoryTheory

open Topology

universe u v

/-!
### DiffCat

Basic definitions and lemmas about the category of diffeological spaces.
-/

section Basic

@[to_additive existing DiffCat]
def DiffCat : Type (u + 1) :=
  Bundled DiffeologicalSpace

namespace DiffCat

instance bundledHom : BundledHom @DSmoothMap where
  toFun := @DSmoothMap.toFun
  id := @DSmoothMap.id
  comp := @DSmoothMap.comp

deriving instance LargeCategory for DiffCat

instance concreteCategory : ConcreteCategory DiffCat :=
  inferInstanceAs <| ConcreteCategory (Bundled DiffeologicalSpace)

instance : CoeSort DiffCat Type* where
  coe X := X.Œ±

instance topologicalSpaceUnbundled (X : DiffCat) : DiffeologicalSpace X :=
  X.str

instance instFunLike (X Y : DiffCat) : FunLike (X ‚ü∂ Y) X Y :=
  inferInstanceAs <| FunLike (DSmoothMap X Y) X Y

-- TODO DSmoothMapClass-Instanz

lemma id_app (X : DiffCat) (x : ‚ÜëX) : (ùüô X : X ‚ü∂ X) x = x := rfl

lemma comp_app {X Y Z : DiffCat} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) (x : X) :
    (f ‚â´ g : X ‚Üí Z) x = g (f x) := rfl

@[simp]
lemma coe_id (X : DiffCat) : (ùüô X : X ‚Üí X) = id := rfl

@[simp]
lemma coe_comp {X Y Z : DiffCat} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    (f ‚â´ g : X ‚Üí Z) = g ‚àò f := rfl

@[simp]
lemma hom_inv_id_apply {X Y : DiffCat} (f : X ‚âÖ Y) (x : X) : f.inv (f.hom x) = x :=
  DFunLike.congr_fun f.hom_inv_id x

@[simp]
lemma inv_hom_id_apply {X Y : DiffCat} (f : X ‚âÖ Y) (y : Y) : f.hom (f.inv y) = y :=
  DFunLike.congr_fun f.inv_hom_id y

/-- Construct a bundled space from the underlying type and the typeclass. -/
def of (X : Type u) [DiffeologicalSpace X] : DiffCat :=
  ‚ü®X, inferInstance‚ü©

@[instance] abbrev diffeologicalSpace_forget
    (X : DiffCat) : DiffeologicalSpace <| (forget DiffCat).obj X :=
  X.str

@[simp]
theorem coe_of (X : Type u) [DiffeologicalSpace X] : (of X : Type u) = X := rfl

-- TODO `coe_of_of`?

instance inhabited : Inhabited DiffCat :=
  ‚ü®Empty,‚ä•‚ü©

def discrete : Type u ‚•§ DiffCat.{u} where
  obj X := ‚ü®X,‚ä•‚ü©
  map f := ‚ü®f,dsmooth_bot‚ü©

def indiscrete : Type u ‚•§ DiffCat.{u} where
  obj X := ‚ü®X,‚ä§‚ü©
  map f := ‚ü®f,dsmooth_top‚ü©

def dTop : DiffCat.{u} ‚•§ TopCat.{u} where
  obj X := ‚ü®X,DTop‚ü©
  map f := ‚ü®f,f.dsmooth.continuous‚ü©

/-- Adjunction `discrete ‚ä£ forget`, adapted from
  `Mathlib.Topology.Category.TopCat.Adjunctions`. -/
@[simps! unit counit]
def discreteForgetAdj : discrete ‚ä£ forget DiffCat.{u} :=
  Adjunction.mkOfUnitCounit
    { unit := { app := fun X => id }
      counit := { app := fun X => ‚ü®id, dsmooth_bot‚ü© } }

/-- Adjunction `forget ‚ä£ indiscrete`, adapted from
  `Mathlib.Topology.Category.TopCat.Adjunctions`. -/
@[simps! unit counit]
def forgetIndiscreteAdj : forget DiffCat.{u} ‚ä£ indiscrete :=
  Adjunction.mkOfUnitCounit
    { unit := { app := fun X => ‚ü®id, dsmooth_top‚ü© }
      counit := { app := fun X => id } }

instance : IsRightAdjoint (forget DiffCat.{u}) :=
  ‚ü®_, discreteForgetAdj‚ü©

instance : IsLeftAdjoint (forget DiffCat.{u}) :=
  ‚ü®_, forgetIndiscreteAdj‚ü©

end DiffCat

end Basic

/-!
### Limits and colimits

The category of diffeological spaces is complete and cocomplete, and the forgetful functor
preserves all limits and colimits. Adapted from `Mathlib.Topology.Category.TopCat.Limits`.
-/
section Limits

namespace DiffCat

open CategoryTheory.Limits

variable {J : Type v} [SmallCategory J]

local notation "forget" => forget DiffCat

/-- A specific choice of limit cone for any `F : J ‚•§ DiffCat`. -/
def limitCone (F : J ‚•§ DiffCat.{max v u}) : Cone F where
  pt := of { u : (j : J) ‚Üí F.obj j | ‚àÄ {i j : J} (f : i ‚ü∂ j), F.map f (u i) = u j }
  œÄ :=
    { app := fun j => ‚ü®fun u => u.val j,DSmooth.comp (dsmooth_apply _) (dsmooth_subtype_val)‚ü©
      naturality := fun X Y f => by
        dsimp [Category.id_comp]
        exact DSmoothMap.ext fun a => (a.2 f).symm }

/-- `DiffCat.limitCone F` is actually a limit cone for the given `F : J ‚•§ DiffCat`. -/
def limitConeIsLimit (F : J ‚•§ DiffCat.{max v u}) : IsLimit (limitCone.{u,v} F) where
  lift S :=
    ‚ü®fun x => ‚ü®fun j => S.œÄ.app _ x, fun f => by dsimp; exact S.w f ‚ñ∏ rfl‚ü©,
    DSmooth.subtype_mk (dsmooth_pi fun j => (S.œÄ.app j).2) fun x i j f => by
      dsimp; exact S.w f ‚ñ∏ rfl‚ü©
  fac S j := by dsimp [limitCone]; rfl
  uniq S m h := DSmoothMap.ext fun a => Subtype.ext <| by simp_rw [‚Üê h]; rfl

instance hasLimitsOfSize : HasLimitsOfSize.{v,v} DiffCat.{max u v} where
  has_limits_of_shape _ := ‚ü®fun F => HasLimit.mk ‚ü®limitCone.{u,v} F,limitConeIsLimit F‚ü©‚ü©

/-- `DiffCat` has all limits, i.e. it is complete. -/
instance hasLimits : HasLimits DiffCat.{u} :=
  hasLimitsOfSize.{u,u}

noncomputable instance forgetPreservesLimitsOfSize : PreservesLimitsOfSize forget :=
  ‚ü®‚ü®fun {F} => preservesLimitOfPreservesLimitCone (limitConeIsLimit.{u,v} F)
      (Types.limitConeIsLimit.{v,u} (F ‚ãô forget))‚ü©‚ü©

/-- The forgetful functor `DiffCat ‚•§ Type` preserves all limits. -/
noncomputable instance forgetPreservesLimits : PreservesLimits forget :=
  forgetPreservesLimitsOfSize.{u,u}

/-- A specific choice of colimit cocone for any `F : J ‚•§ DiffCat`. -/
noncomputable def colimitCocone (F : J ‚•§ DiffCat.{max v u}) : Cocone F where
  pt := ‚ü®(Types.TypeMax.colimitCocone.{v,u} (F ‚ãô forget)).pt,
          ‚®Ü j, (F.obj j).str.coinduced ((Types.TypeMax.colimitCocone (F ‚ãô forget)).Œπ.app j)‚ü©
  Œπ :=
    { app := fun j =>
        ‚ü®(Types.TypeMax.colimitCocone (F ‚ãô forget)).Œπ.app j, dsmooth_iff_coinduced_le.mpr <|
          le_iSup (fun j => DiffeologicalSpace.coinduced
            ((Types.TypeMax.colimitCocone (F ‚ãô forget)).Œπ.app j) (F.obj j).str) j‚ü©
      naturality := fun _ _ f =>
        DSmoothMap.coe_injective ((Types.TypeMax.colimitCocone (F ‚ãô forget)).Œπ.naturality f) }


/-- `DiffCat.colimitCocone F` is actually a colimit cocone for the given `F : J ‚•§ DiffCat`. -/
def colimitCoconeIsColimit (F : J ‚•§ DiffCat.{max v u}) : IsColimit (colimitCocone F) := by
  refine IsColimit.ofFaithful forget (Types.TypeMax.colimitCoconeIsColimit.{v,u} _) (fun s =>
      ‚ü®Quot.lift (fun p => (Functor.mapCocone forget s).Œπ.app p.fst p.snd) ?_, ?_‚ü©) fun s => rfl
  ¬∑ intro _ _ ‚ü®_, h‚ü©; simp [h,‚Üêcomp_apply',s.Œπ.naturality]
  ¬∑ exact dsmooth_iff_le_induced.mpr
      (iSup_le fun j => DiffeologicalSpace.coinduced_le_iff_le_induced.mp <|
        DiffeologicalSpace.coinduced_compose.symm ‚ñ∏ (s.Œπ.app j).dsmooth.coinduced_le)

instance hasColimitsOfSize : HasColimitsOfSize.{v,v} DiffCat.{max v u} where
  has_colimits_of_shape _ := ‚ü®fun F => HasColimit.mk ‚ü®colimitCocone F,colimitCoconeIsColimit F‚ü©‚ü©

/-- `DiffCat` has all colimits, i.e. it is cocomplete. -/
instance hasColimits : HasColimits DiffCat.{u} :=
  hasColimitsOfSize.{u,u}

noncomputable instance forgetPreservesColimitsOfSize : PreservesColimitsOfSize forget :=
  ‚ü®‚ü®fun {F} => preservesColimitOfPreservesColimitCocone (colimitCoconeIsColimit.{u,v} F)
    (Types.TypeMax.colimitCoconeIsColimit.{v,u} (F ‚ãô forget))‚ü©‚ü©

/-- The forgetful functor `DiffCat ‚•§ Type` preserves all colimits. -/
noncomputable instance forgetPreservesColimits : PreservesColimits forget :=
  forgetPreservesColimitsOfSize.{u,u}

end DiffCat

end Limits
