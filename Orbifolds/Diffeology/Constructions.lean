import Orbifolds.Diffeology.Induced
import Mathlib.Analysis.InnerProductSpace.Calculus

/-!
# Constructions of diffeological spaces
Mostly based on `Mathlib.Topology.Constructions`.
-/

set_option autoImplicit false

open TopologicalSpace Set

open Topology

universe u v

section Constructions

instance instDiffeologicalSpaceSubtype {X : Type*} [DiffeologicalSpace X] {p : X ‚Üí Prop} :
    DiffeologicalSpace (Subtype p) :=
  DiffeologicalSpace.induced ((‚Üë) : _ ‚Üí X) inferInstance

instance {X : Type*} {r : X ‚Üí X ‚Üí Prop} [d : DiffeologicalSpace X] :
    DiffeologicalSpace (Quot r) :=
  d.coinduced (Quot.mk r)

instance instDiffeologicalSpaceQuotient {X : Type*} {s : Setoid X} [d : DiffeologicalSpace X] :
    DiffeologicalSpace (Quotient s) :=
  d.coinduced Quotient.mk'

instance instDiffeologicalSpaceProd {X Y : Type*} [dX : DiffeologicalSpace X]
    [dY : DiffeologicalSpace Y] : DiffeologicalSpace (X √ó Y) :=
  dX.induced Prod.fst ‚äì dY.induced Prod.snd

instance Pi.diffeologicalSpace {Œπ : Type*} {Y : Œπ ‚Üí Type*}
    [(i : Œπ) ‚Üí DiffeologicalSpace (Y i)] : DiffeologicalSpace ((i : Œπ) ‚Üí Y i) where
  plots n := {p | ‚àÄ i, IsPlot ((fun y => y i) ‚àò p)}
  constant_plots _ i := isPlot_const
  plot_reparam {n m p f} := fun hp hf i => by
    exact Function.comp.assoc _ _ _ ‚ñ∏ isPlot_reparam (hp i) hf
  locality := by sorry

instance ULift.diffeologicalSpace {X : Type u} [t : DiffeologicalSpace X] :
    DiffeologicalSpace (ULift.{v, u} X) :=
  t.induced ULift.down

end Constructions

section OrderDual

variable {X : Type*} [DiffeologicalSpace X]

open OrderDual

instance : DiffeologicalSpace X·µí·µà := ‚ÄπDiffeologicalSpace X‚Ä∫

theorem dsmooth_toDual : DSmooth (toDual : X ‚Üí X·µí·µà) := dsmooth_id

theorem dsmooth_ofDual : DSmooth (ofDual : X·µí·µà ‚Üí X) := dsmooth_id

theorem induction_toDual : Induction (toDual : X ‚Üí X·µí·µà) := induction_id

theorem induction_ofDual : Induction (ofDual : X·µí·µà ‚Üí X) := induction_id

theorem subduction_toDual : Subduction (toDual : X ‚Üí X·µí·µà) := subduction_id

theorem subduction_ofDual : Subduction (ofDual : X·µí·µà ‚Üí X) := subduction_id

end OrderDual

section Pi

variable {Œπ : Type*} {Y : Œπ ‚Üí Type*} [(i : Œπ) ‚Üí DiffeologicalSpace (Y i)]
  {X : Type*} [DiffeologicalSpace X] {f : X ‚Üí ((i : Œπ) ‚Üí Y i)}

theorem dsmooth_pi_iff : DSmooth f ‚Üî ‚àÄ i, DSmooth fun x => f x i := by
  simp only [dsmooth_iff,@forall_comm Œπ _ _]; rfl

@[fun_prop]
theorem dsmooth_pi (h : ‚àÄ i, DSmooth fun a => f a i) : DSmooth f :=
  dsmooth_pi_iff.2 h

@[fun_prop]
theorem dsmooth_apply (i : Œπ) : DSmooth fun p : (i : Œπ) ‚Üí Y i => p i :=
  dsmooth_pi_iff.1 dsmooth_id i

-- TODO. something like this should be true, but I haven't yet figured out the exact details.
instance [Fintype Œπ] [(i : Œπ) ‚Üí TopologicalSpace (Y i)] [(i : Œπ) ‚Üí LocallyCompactSpace (Y i)]
    [(i : Œπ) ‚Üí DTopCompatible (Y i)] : DTopCompatible ((i : Œπ) ‚Üí Y i) := ‚ü®by
  ext s; rw [isOpen_pi_iff',isOpen_iff_preimages_plots]
  refine' ‚ü®fun h => _, fun h n p hp => _‚ü©
  all_goals sorry‚ü©

instance {Œπ : Type*} [Fintype Œπ] {Y : Œπ ‚Üí Type*} [(i : Œπ) ‚Üí NormedAddCommGroup (Y i)]
    [(i : Œπ) ‚Üí NormedSpace ‚Ñù (Y i)] [(i : Œπ) ‚Üí DiffeologicalSpace (Y i)]
    [(i : Œπ) ‚Üí ContDiffCompatible (Y i)] : ContDiffCompatible ((i : Œπ) ‚Üí Y i) :=
  ‚ü®by simp_rw [contDiff_pi,‚ÜêContDiffCompatible.isPlot_iff]; rfl‚ü©

end Pi

section Subtype

variable {X : Type*} [DiffeologicalSpace X] {s : Set X} {p : X ‚Üí Prop}
  {Y : Type*} [DiffeologicalSpace Y]

theorem induction_subtype_val : Induction ((‚Üë) : s ‚Üí X) :=
  ‚ü®Subtype.coe_injective,rfl‚ü©

theorem Induction.of_codRestrict {f : X ‚Üí Y} {t : Set Y} (ht : ‚àÄ x, f x ‚àà t)
    (hf : Induction (t.codRestrict f ht)) : Induction f :=
  induction_subtype_val.comp hf

theorem dsmooth_subtype_val : DSmooth ((‚Üë) : s ‚Üí X) :=
  induction_subtype_val.dsmooth

theorem DSmooth.subtype_val {f : Y ‚Üí Subtype p} (hf : DSmooth f) :
    DSmooth fun x => (f x : X) :=
  dsmooth_subtype_val.comp hf

theorem DSmooth.subtype_mk {f : Y ‚Üí X} (hf : DSmooth f) (hp : ‚àÄ x, p (f x)) :
    DSmooth fun x => (‚ü®f x, hp x‚ü© : Subtype p) :=
  hf

theorem DSmooth.subtype_map {f : X ‚Üí Y} (h : DSmooth f) {q : Y ‚Üí Prop}
    (hpq : ‚àÄ x, p x ‚Üí q (f x)) : DSmooth (Subtype.map f hpq) :=
  (h.comp dsmooth_subtype_val).subtype_mk _

theorem dsmooth_inclusion {s t : Set X} (h : s ‚äÜ t) : DSmooth (inclusion h) :=
  dsmooth_id.subtype_map h

theorem DSmooth.codRestrict {f : X ‚Üí Y} {s : Set Y} (hf : DSmooth f) (hs : ‚àÄ a, f a ‚àà s) :
    DSmooth (s.codRestrict f hs) :=
  hf.subtype_mk hs

theorem DSmooth.restrict {f : X ‚Üí Y} {s : Set X} {t : Set Y} (h1 : MapsTo f s t)
    (h2 : DSmooth f) : DSmooth (h1.restrict f s t) :=
  (h2.comp dsmooth_subtype_val).codRestrict _

theorem DSmooth.restrictPreimage {f : X ‚Üí Y} {s : Set Y} (h : DSmooth f) :
    DSmooth (s.restrictPreimage f) :=
  h.restrict _

theorem Induction.codRestrict {f : X ‚Üí Y} (hf : Induction f) {s : Set Y} (hs : ‚àÄ x, f x ‚àà s) :
    Induction (s.codRestrict f hs) :=
  Induction.of_comp' (hf.dsmooth.codRestrict hs) dsmooth_subtype_val hf

-- TODO: move to mathlib.
theorem ContDiffOn.comp_contDiff {ùïú E F G : Type*} [NontriviallyNormedField ùïú]
    [NormedAddCommGroup E] [NormedSpace ùïú E] [NormedAddCommGroup F] [NormedSpace ùïú F]
    [NormedAddCommGroup G] [NormedSpace ùïú G] {n : ‚Ñï‚àû} {s : Set F} {f : E ‚Üí F} {g : F ‚Üí G}
    (hg : ContDiffOn ùïú n g s) (hf : ContDiff ùïú n f) (hs : ‚àÄ x, f x ‚àà s) :
    ContDiff ùïú n (g ‚àò f) :=
  contDiffOn_univ.1 <| hg.comp (contDiffOn_univ.2 hf) fun x _ => hs x

theorem ContDiffOn.dsmooth_restrict [NormedAddCommGroup X] [NormedSpace ‚Ñù X] [ContDiffCompatible X]
    [NormedAddCommGroup Y] [NormedSpace ‚Ñù Y] [ContDiffCompatible Y]
    {f : X ‚Üí Y} (hf : ContDiffOn ‚Ñù ‚ä§ f s) : DSmooth (s.restrict f) := by
  refine' fun n p hp => isPlot_iff_contDiff.2 _
  rw [restrict_eq,Function.comp.assoc]
  exact hf.comp_contDiff (dsmooth_subtype_val.comp hp.dsmooth).contDiff fun x => (p x).2

-- TODO: relax dimensionality hypothesis?
open PartialHomeomorph in
/-- On an open subset, a function is smooth in the usual sense iff it is smooth diffeologically. -/
theorem IsOpen.dsmooth_iff_contDiffOn [NormedAddCommGroup X] [InnerProductSpace ‚Ñù X]
    [ContDiffCompatible X] [FiniteDimensional ‚Ñù X]
    [NormedAddCommGroup Y] [NormedSpace ‚Ñù Y] [ContDiffCompatible Y]
    {f : X ‚Üí Y} (hs : IsOpen s) : DSmooth (s.restrict f) ‚Üî ContDiffOn ‚Ñù ‚ä§ f s := by
  refine' ‚ü®fun hf x hxs => _,ContDiffOn.dsmooth_restrict‚ü©
  let ‚ü®Œµ,hŒµ,hŒµ'‚ü© := Metric.isOpen_iff.1 hs x hxs
  refine' ContDiffWithinAt.mono_of_mem (s := Metric.ball x Œµ) _ <| mem_nhdsWithin.2
    ‚ü®_,Metric.isOpen_ball,Metric.mem_ball_self hŒµ,inter_subset_left _ _‚ü©
  suffices h : ContDiffOn ‚Ñù ‚ä§ f (Metric.ball x Œµ) by exact h x (Metric.mem_ball_self hŒµ)
  let e := univUnitBall.trans' (unitBallBall x Œµ hŒµ) rfl
  have he : DSmooth e :=
    (contDiff_unitBallBall hŒµ).dsmooth.comp contDiff_univUnitBall.dsmooth
  let hes x : e x ‚àà s := hŒµ' (e.map_source (mem_univ x))
  refine' ContDiffOn.congr (f := (f ‚àò e) ‚àò e.symm) _ fun x hx => by
    rw [Function.comp_apply]; exact congrArg _ (e.right_inv' hx).symm
  refine' ContDiff.comp_contDiffOn (DSmooth.contDiff _) _
  ¬∑ rw [‚Üêrestrict_comp_codRestrict (b := s) hes]
    exact hf.comp (DSmooth.codRestrict he hes)
  ¬∑ exact contDiffOn_univUnitBall_symm.comp (contDiff_unitBallBall_symm hŒµ).contDiffOn
      (fun _ => (unitBallBall x Œµ hŒµ).symm.map_source)

-- TODO: move to Mathlib.Topology.Constructions
theorem IsOpenMap.subtype_mk {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    {s : Set X} {f : Y ‚Üí X} (hf : IsOpenMap f) (hfs : ‚àÄ x, f x ‚àà s) :
    IsOpenMap fun x => (‚ü®f x, hfs x‚ü© : Subtype s) := fun u hu => by
  convert (hf u hu).preimage continuous_subtype_val
  exact Set.ext fun x => exists_congr fun x' => and_congr_right' Subtype.ext_iff

-- TODO: move to Mathlib.Topology.Constructions
theorem IsOpen.isOpenMap_subtype_map {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    {s : Set X} {t : Set Y} {f : X ‚Üí Y} (hs : IsOpen s) (hf : IsOpenMap f)
    (hst : ‚àÄ x, s x ‚Üí t (f x)) : IsOpenMap (Subtype.map f hst) :=
  (hf.comp hs.isOpenMap_subtype_val).subtype_mk _

-- TODO: move to Mathlib.Topology.Constructions
theorem IsOpen.isOpenMap_inclusion {X : Type*} [TopologicalSpace X] {s t : Set X}
    (hs : IsOpen s) (h : s ‚äÜ t) : IsOpenMap (inclusion h) :=
  hs.isOpenMap_subtype_map IsOpenMap.id h

-- TODO: move to Mathlib.Topology.Constructions
theorem IsOpenMap.codRestrict {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    {f : X ‚Üí Y} {s : Set Y} (hf : IsOpenMap f) (hs : ‚àÄ a, f a ‚àà s) :
    IsOpenMap (s.codRestrict f hs) :=
  hf.subtype_mk hs

/-- The D-topology is also characterised by the smooth maps `u ‚Üí X` for open `u`. -/
lemma isOpen_iff_preimages_plots' {s : Set X} : IsOpen[DTop] s ‚Üî
    ‚àÄ (n : ‚Ñï) (u : Set (Eucl n)) (p : u ‚Üí X), IsOpen u ‚Üí DSmooth p ‚Üí IsOpen (p ‚Åª¬π' s) := by
  rw [isOpen_iff_preimages_plots]
  refine' ‚ü®fun hs n u p hu hp => _,fun hs n p hp => _‚ü©
  ¬∑ rw [‚ÜêisOpen_iff_preimages_plots] at hs
    have := dTop_induced_comm ((Subtype.range_val (s := u)).symm ‚ñ∏ hu)
    convert @IsOpen.preimage _ _ DTop DTop p hp.continuous _ hs
    exact (dTop_induced_comm ((Subtype.range_val (s := u)).symm ‚ñ∏ hu)).symm
  ¬∑ let e := Homeomorph.Set.univ (Fin n ‚Üí ‚Ñù)
    rw [‚Üêe.isOpen_preimage,‚Üêpreimage_comp]
    exact hs n _ (p ‚àò e) isOpen_univ (hp.dsmooth.comp dsmooth_subtype_val)

/-- On open subsets, the D-topology and subspace topology agree. -/
protected theorem IsOpen.dTopCompatible [TopologicalSpace X] [DTopCompatible X] (hs : IsOpen s) :
    DTopCompatible s := ‚ü®by
  exact (dTop_eq X) ‚ñ∏ dTop_induced_comm (Subtype.range_coe.symm ‚ñ∏ (dTop_eq X) ‚ñ∏ hs)‚ü©

instance [TopologicalSpace X] [DTopCompatible X] [h : Fact (IsOpen s)] : DTopCompatible s :=
  h.out.dTopCompatible

/-- Smoothness can also be characterised as preserving smooth maps `u ‚Üí X` for open `u`.-/
theorem dsmooth_iff' {f : X ‚Üí Y} : DSmooth f ‚Üî
    ‚àÄ (n : ‚Ñï) (u : Set (Eucl n)) (p : u ‚Üí X), IsOpen u ‚Üí DSmooth p ‚Üí DSmooth (f ‚àò p) := by
  refine' ‚ü®fun hf n u p _ hp => hf.comp hp,fun hf n p hp => _‚ü©
  rw [‚ÜêFunction.comp_id (f ‚àò p),‚Üê(Homeomorph.Set.univ _).self_comp_symm,‚ÜêFunction.comp.assoc]
  exact ((hf n _ _ isOpen_univ (hp.dsmooth.comp dsmooth_subtype_val)).comp
    (dsmooth_id.subtype_mk _)).isPlot

/-- The locality axiom of diffeologies. Restated here in terms of subspace diffeologies. -/
theorem isPlot_iff_locally_dsmooth {n : ‚Ñï} {p : Eucl n ‚Üí X} : IsPlot p ‚Üî
    ‚àÄ x : Eucl n, ‚àÉ u, IsOpen u ‚àß x ‚àà u ‚àß  DSmooth (u.restrict p) := by
  refine' ‚ü®fun hp x => ‚ü®_,isOpen_univ,mem_univ x,hp.dsmooth.comp dsmooth_subtype_val‚ü©,_‚ü©
  refine' fun h => DiffeologicalSpace.locality fun x => _
  let ‚ü®u,hu,hxu,hu'‚ü© := h x
  refine' ‚ü®u,hu,hxu,fun {m f} hfu hf => u.restrict_comp_codRestrict hfu ‚ñ∏ _‚ü©
  exact  (hu' _ _ (hf.dsmooth.codRestrict hfu).isPlot)

theorem dsmooth_iff_locally_dsmooth {f : X ‚Üí Y} : DSmooth f ‚Üî
    ‚àÄ x : X, ‚àÉ u : Set X, IsOpen[DTop] u ‚àß x ‚àà u ‚àß DSmooth (u.restrict f) := by
  refine' ‚ü®fun hf x => ‚ü®_,by simp,mem_univ x,hf.comp dsmooth_subtype_val‚ü©,fun h n p hp => _‚ü©
  refine' isPlot_iff_locally_dsmooth.2  fun x => _
  let ‚ü®u,hu,hxu,hu'‚ü© := h (p x)
  refine' ‚ü®p ‚Åª¬π' u,@IsOpen.preimage _ _ _ DTop p (dTop_eq (Eucl n) ‚ñ∏ hp.continuous) u hu,hxu,_‚ü©
  exact hu'.comp hp.dsmooth.restrictPreimage

/-- Any D-locally constant map is smooth. -/
theorem IsLocallyConstant.dsmooth {f : X ‚Üí Y} (hf : @IsLocallyConstant _ _ DTop f) :
    DSmooth f := by
  refine' dsmooth_iff_locally_dsmooth.2 fun x => Exists.imp (fun u ‚ü®hu,hxu,hu'‚ü© => ‚ü®hu,hxu,_‚ü©)
    (@IsLocallyConstant.exists_open _ _ DTop f hf x)
  rw [show u.restrict f = fun _ => f x by ext x'; exact hu' x'.1 x'.2]
  exact dsmooth_const

end Subtype

section DTop

/-- The indiscrete diffeology is the one for which every map is a plot. -/
theorem DiffeologicalSpace.eq_top_iff {X : Type*} {dX : DiffeologicalSpace X} :
    dX = ‚ä§ ‚Üî ‚àÄ n (p : Eucl n ‚Üí X), IsPlot p :=
  ‚ü®fun h _ _ => h ‚ñ∏ trivial,fun h => IsTop.eq_top fun _ => le_iff'.2 fun n p _ => h n p‚ü©

open PartialHomeomorph in
/-- The discrete diffeology is the one with only the constant maps as plots. -/
theorem DiffeologicalSpace.eq_bot_iff {X : Type*} {dX : DiffeologicalSpace X} :
    dX = ‚ä• ‚Üî ‚àÄ n (p : Eucl n ‚Üí X), IsPlot p ‚Üí ‚àÉ x, p = fun _ => x := by
  refine' ‚ü®fun h n p => fun hp => _,fun h => IsBot.eq_bot fun d => _‚ü©
  ¬∑ let d : DiffeologicalSpace X := {
      plots := fun n => {p | ‚àÉ x, p = fun _ => x}
      constant_plots := fun x => ‚ü®x,rfl‚ü©
      plot_reparam := fun ‚ü®x,hx‚ü© _ => ‚ü®x,by rw [hx]; rfl‚ü©
      locality := fun {n p} h => by
        have := Nonempty.map p inferInstance
        refine' IsLocallyConstant.exists_eq_const <| (IsLocallyConstant.iff_exists_open p).2 _
        intro x; let ‚ü®u,hu,hxu,hu'‚ü© := h x; let ‚ü®Œµ,hŒµ,hŒµ'‚ü© := Metric.isOpen_iff.1 hu x hxu
        refine' ‚ü®Metric.ball x Œµ,Metric.isOpen_ball,Metric.mem_ball_self hŒµ,_‚ü©
        let e : Eucl n ‚âÉ‚Çú Metric.ball x Œµ := (Homeomorph.Set.univ _).symm.trans <|
          univUnitBall.toHomeomorphSourceTarget.trans
            (unitBallBall x Œµ hŒµ).toHomeomorphSourceTarget
        have he : DSmooth (((‚Üë) : _ ‚Üí Eucl n) ‚àò e) :=
          (contDiff_unitBallBall hŒµ).dsmooth.comp contDiff_univUnitBall.dsmooth
        let ‚ü®x'',hx''‚ü© := @hu' n ((‚Üë) ‚àò e) (fun x'' => hŒµ' (e x'').2) he.contDiff
        suffices h : ‚àÄ x' : Metric.ball x Œµ, p x' = x'' by
          intro x' hx'; rw [h ‚ü®x',hx'‚ü©,h ‚ü®x,Metric.mem_ball_self hŒµ‚ü©]
        intro x'
        rw [‚ÜêFunction.comp_apply (f := p),‚ÜêFunction.comp_id (p ‚àò _),‚Üêe.self_comp_symm,
          ‚ÜêFunction.comp.assoc,Function.comp.assoc p,hx'',Function.comp_apply]}
    exact le_iff'.1 (h.symm ‚ñ∏ bot_le (a := d)) n p hp
  ¬∑ exact le_iff'.2 fun n p hp => (h n p hp).choose_spec ‚ñ∏ isPlot_const

theorem dTop_mono {X : Type*} {d‚ÇÅ d‚ÇÇ : DiffeologicalSpace X} (h : d‚ÇÅ ‚â§ d‚ÇÇ) :
    DTop[d‚ÇÅ] ‚â§ DTop[d‚ÇÇ] := by
  refine' TopologicalSpace.le_def.2 fun u hu => _
  rw [@isOpen_iff_preimages_plots] at hu ‚ä¢
  rw [DiffeologicalSpace.le_iff'] at h
  exact fun n p => hu n p ‚àò h n p

/-- The D-topology of the indiscrete diffeology is indiscrete. -/
theorem dTop_top {X : Type*} : DTop[‚ä§] = (‚ä§ : TopologicalSpace X) := by
  let f : X ‚Üí Unit := default
  have h : @DTop Unit ‚ä§ = ‚ä• := Unique.eq_default _
  rw [‚ÜêDiffeologicalSpace.induced_top (f := f), dTop_induced_comm (by rw [h]; trivial),
    h.trans (Unique.default_eq ‚ä§),induced_top]

/-- The D-topology of the discrete diffeology is discrete. -/
theorem dTop_bot {X : Type*} : DTop[‚ä•] = (‚ä• : TopologicalSpace X) := by
  ext u; refine' ‚ü®fun _ => trivial,fun _ => _‚ü©
  rw [@isOpen_iff_preimages_plots _ ‚ä• u]; intro n p hp
  let ‚ü®x,hx‚ü© := DiffeologicalSpace.eq_bot_iff.1 rfl n p hp
  by_cases h : x ‚àà u; all_goals simp [hx,h]

/-- The discrete diffeologoy is the only diffeology whose D-topology is discrete.
  Note that the corresponding statement for indiscrete spaces is false. -/
theorem dTop_eq_bot_iff {X : Type*} {dX : DiffeologicalSpace X} : DTop[dX] = ‚ä• ‚Üî dX = ‚ä• := by
  refine' ‚ü®fun h => _,fun h => by rw [h,dTop_bot]‚ü©
  refine' (dX.eq_bot_iff).2 fun n p hp => ‚ü®p 0,funext fun x => _‚ü©
  exact @PreconnectedSpace.constant _ _ X ‚ä• (discreteTopology_bot X) inferInstance
    p (h ‚ñ∏ hp.continuous) _ _

/-- A map from an indiscrete space is smooth iff its range is indiscrete.
  Note that this can't be characterised purely topologically, since it might be the case that
  all involved D-topologies are indiscrete but the diffeologies are not. -/
theorem dsmooth_top_iff_indiscrete_range {X Y : Type*} {dY : DiffeologicalSpace Y} {f : X ‚Üí Y} :
    DSmooth[‚ä§,dY] f ‚Üî @instDiffeologicalSpaceSubtype Y dY (Set.range f) = ‚ä§ := by
  let _ : DiffeologicalSpace X := ‚ä§
  refine' ‚ü®fun hf => _,fun h => _‚ü©
  ¬∑ refine' DiffeologicalSpace.eq_top_iff.2 fun n p => _
    have hf' : DSmooth (Set.rangeFactorization f) := hf.codRestrict mem_range_self
    let ‚ü®g,hg‚ü© := (Set.surjective_onto_range (f := f)).hasRightInverse
    have h := hf' n (g ‚àò p) trivial
    rw [‚ÜêFunction.comp.assoc,hg.id,Function.id_comp] at h; exact h
  ¬∑ exact dsmooth_subtype_val.comp (h ‚ñ∏ dsmooth_top : DSmooth (Set.rangeFactorization f))

/-- A map to an discrete space is smooth iff it is D-locally constant. -/
theorem dsmooth_bot_iff_isLocallyConstant {X Y : Type*} {dX : DiffeologicalSpace X} {f : X ‚Üí Y} :
    DSmooth[dX,‚ä•] f ‚Üî @IsLocallyConstant _ _ DTop[dX] f:= by
  refine' ‚ü®fun hf _ => _,@IsLocallyConstant.dsmooth _ dX Y ‚ä• _‚ü©
  exact @IsOpen.preimage _ Y DTop[dX] ‚ä• _ (dTop_bot ‚ñ∏ @DSmooth.continuous _ Y dX ‚ä• _ hf) _ trivial

open PartialHomeomorph in
/-- A map is a plot in the coinduced diffeology iff it is constant or lifts locally. -/
theorem isPlot_coinduced_iff {X Y : Type*} {dX : DiffeologicalSpace X} {f : X ‚Üí Y}
    {n : ‚Ñï} {p : Eucl n ‚Üí Y} : IsPlot[dX.coinduced f] p ‚Üî (‚àÉ y, p = fun _ => y) ‚à®
    ‚àÄ x : Eucl n, ‚àÉ u, IsOpen u ‚àß x ‚àà u ‚àß ‚àÉ p' : u ‚Üí X, DSmooth p' ‚àß p ‚àò (‚Üë) = f ‚àò p' := by
  let dY := dX.coinduced f
  refine' ‚ü®fun hp => _,Or.rec (fun ‚ü®y,hy‚ü© => hy ‚ñ∏ isPlot_const) fun h => _‚ü©
  ¬∑ let d : DiffeologicalSpace Y := {
      plots := fun n => {p | (‚àÉ y, p = fun _ => y) ‚à®
        ‚àÄ x : Eucl n, ‚àÉ u , IsOpen u ‚àß x ‚àà u ‚àß ‚àÉ p' : u ‚Üí X, DSmooth p' ‚àß p ‚àò (‚Üë) = f ‚àò p'}
      constant_plots := fun x => Or.inl ‚ü®x,rfl‚ü©
      plot_reparam := fun {n m p g} hp hg => Or.rec (fun ‚ü®y,hy‚ü© => Or.inl ‚ü®y,hy ‚ñ∏ rfl‚ü©)
        (fun h => Or.inr fun x => (by
          let ‚ü®u,hu,hxu,p',hp',hp''‚ü© := h (g x)
          refine' ‚ü®g ‚Åª¬π' u,hu.preimage hg.continuous,hxu,p' ‚àò u.restrictPreimage g,
          hp'.comp hg.dsmooth.restrictPreimage,_‚ü©
          simp_rw [‚ÜêFunction.comp.assoc,‚Üêhp'',Function.comp.assoc]; rfl)) hp
      locality := fun {n p} h => by
        replace h : ‚àÄ x : Eucl n, ‚àÉ u, IsOpen u ‚àß x ‚àà u ‚àß
            (p x ‚àâ range f ‚Üí u.restrict p = fun _ => p x) ‚àß
            (p x ‚àà range f ‚Üí ‚àÉ p' : u ‚Üí X, DSmooth p' ‚àß p ‚àò (‚Üë) = f ‚àò p') := fun x => by
          let ‚ü®u,hu,hxu,hu'‚ü© := h x; let ‚ü®Œµ,hŒµ,hŒµ'‚ü© := Metric.isOpen_iff.1 hu x hxu
          have hx := Metric.mem_ball_self hŒµ (x := x)
          let e : Eucl n ‚âÉ‚Çú Metric.ball x Œµ := (Homeomorph.Set.univ _).symm.trans <|
            univUnitBall.toHomeomorphSourceTarget.trans
              (unitBallBall x Œµ hŒµ).toHomeomorphSourceTarget
          have he : DSmooth (((‚Üë) : _ ‚Üí Eucl n) ‚àò e) :=
            (contDiff_unitBallBall hŒµ).dsmooth.comp
              contDiff_univUnitBall.dsmooth
          have he' : DSmooth e.symm := by
            have h‚ÇÅ : DSmooth (univUnitBall (E := Eucl n)).toHomeomorphSourceTarget.symm :=
              ContDiffOn.dsmooth_restrict (contDiffOn_univUnitBall_symm (n := ‚ä§) (E := Eucl n))
            have h‚ÇÇ : DSmooth (unitBallBall x Œµ hŒµ).toHomeomorphSourceTarget.symm :=
              (contDiff_unitBallBall_symm (n := ‚ä§) (c := x) hŒµ).dsmooth.restrict
                ((unitBallBall x Œµ hŒµ).symm_mapsTo)
            exact (dsmooth_subtype_val.comp (h‚ÇÅ.comp h‚ÇÇ):)
          specialize hu' (by exact fun x'' => hŒµ' (e x'').2) he.contDiff; dsimp at hu'
          obtain ‚ü®x',hx'‚ü© | hpx := @or_not (p x ‚àà range f)
          ¬∑ obtain ‚ü®y,hy‚ü© | hu' := hu'
            ¬∑ refine' ‚ü®_,Metric.isOpen_ball,hx,fun h => (h ‚ü®x',hx'‚ü©).elim,fun _ => _‚ü©
              have h := congrFun hy (e.symm ‚ü®x,hx‚ü©)
              simp_rw [Function.comp_apply, Homeomorph.apply_symm_apply] at h
              refine' ‚ü®fun _ => x',dsmooth_const,_‚ü©
              rw [‚ÜêFunction.comp_id (f := Subtype.val),‚ÜêHomeomorph.self_comp_symm e,
                ‚ÜêFunction.comp.assoc _ e,‚ÜêFunction.comp.assoc,hy,‚Üêh,‚Üêhx']; rfl
            ¬∑ let ‚ü®v,hv,hxv,p',hp'‚ü© := hu' (e.symm ‚ü®x,hx‚ü©)
              refine' ‚ü®Subtype.val '' (e.symm ‚Åª¬π' v),Metric.isOpen_ball.isOpenMap_subtype_val _
                (hv.preimage e.symm.continuous),‚ü®_,hxv,by simp‚ü©,fun h => (h ‚ü®x',hx'‚ü©).elim,fun _ => _‚ü©
              use p' ‚àò (v.restrictPreimage e.symm) ‚àò (fun x => ‚ü®‚ü®x.1,
                (Subtype.range_val ‚ñ∏ image_subset_range _ _ x.2 :)‚ü©,
                  Subtype.val_injective.mem_set_image.1 x.2‚ü©)
              refine' ‚ü®hp'.1.comp <| he'.restrictPreimage.comp <| DSmooth.subtype_mk
                (DSmooth.subtype_mk dsmooth_subtype_val _) _,_‚ü©
              rw [‚ÜêFunction.comp.assoc,‚Üêhp'.2]; ext x; simp
          ¬∑ refine' ‚ü®_,Metric.isOpen_ball,hx,fun _ => _,fun h => (hpx h).elim‚ü©
            let ‚ü®y,hy‚ü© := (or_iff_left (fun h => hpx <| by
              let ‚ü®v,_,hxv,p',_,hp'‚ü© := h (e.symm ‚ü®x,hx‚ü©)
              have h := congrFun hp' ‚ü®_,hxv‚ü©
              simp_rw [Function.comp_apply, Homeomorph.apply_symm_apply] at h
              exact ‚ü®_,h.symm‚ü©)).1 hu'
            rw [restrict_eq,‚ÜêFunction.comp_id (f := Subtype.val),‚ÜêHomeomorph.self_comp_symm e,
              ‚ÜêFunction.comp.assoc _ e,‚ÜêFunction.comp.assoc,hy]
            have h := congrFun hy (e.symm ‚ü®x,hx‚ü©)
            simp_rw [Function.comp_apply, Homeomorph.apply_symm_apply] at h
            rw [h]; rfl
        have h' : IsClopen (p ‚Åª¬π' (range f)) := by
          refine' ‚ü®‚ü®isOpen_iff_mem_nhds.2 fun x hx => _‚ü©,isOpen_iff_mem_nhds.2 fun x hx => _‚ü©
          all_goals let ‚ü®u,hu,hxu,hu'‚ü© := h x; rw [mem_nhds_iff]; refine' ‚ü®u,_,hu,hxu‚ü©
          ¬∑ refine' fun x' hx' => (_ : p x' ‚àâ range f)
            rw [(by exact congrFun (hu'.1 hx) ‚ü®x',hx'‚ü© : p x' = p x)]; exact hx
          ¬∑ let ‚ü®p',_,(hp' : u.restrict p = _)‚ü© := hu'.2 hx
            rw [‚Üêimage_subset_iff,‚Üêrange_restrict,hp']; exact range_comp_subset_range p' f
        cases' isClopen_iff.1 h' with h' h'
        ¬∑ left; have := Nonempty.map p inferInstance
          refine' IsLocallyConstant.exists_eq_const <| (IsLocallyConstant.iff_exists_open p).2 _
          intro x; let ‚ü®u,hu,hxu,hu',_‚ü© := h x
          exact ‚ü®u,hu,hxu,fun x' hx' => congrFun (hu' (h'.symm ‚ñ∏ not_mem_empty x:)) ‚ü®x',hx'‚ü©‚ü©
        ¬∑ right; intro x; let ‚ü®u,hu,hxu,_,hu'‚ü© := h x
          exact ‚ü®u,hu,hxu,hu' (h'.symm ‚ñ∏ mem_univ x:)‚ü©
    }
    have hd : dY ‚â§ d := sInf_le fun n p hp => Or.inr fun x => ‚ü®_,isOpen_univ,mem_univ x,
      p ‚àò (Equiv.Set.univ _),hp.dsmooth.comp dsmooth_subtype_val,rfl‚ü©
    exact DiffeologicalSpace.le_iff'.1 hd n p hp
  ¬∑ refine' isPlot_iff_locally_dsmooth.2 fun x => Exists.imp (fun u ‚ü®hu,hxu,p',hp'‚ü© => _) (h x)
    rw [Set.restrict_eq,hp'.2]
    exact ‚ü®hu,hxu,dsmooth_coinduced_rng.comp hp'.1‚ü©

/-- For surjective functions, the plots of the coinduced diffeology are precicely those that
  locally lift. -/
theorem Function.Surjective.isPlot_coinduced_iff {X Y : Type*} {dX : DiffeologicalSpace X}
    {f : X ‚Üí Y} (hf : Function.Surjective f) {n : ‚Ñï} {p : Eucl n ‚Üí Y} : IsPlot[dX.coinduced f] p ‚Üî
    ‚àÄ x : Eucl n, ‚àÉ u, IsOpen u ‚àß x ‚àà u ‚àß ‚àÉ p' : u ‚Üí X, DSmooth p' ‚àß p ‚àò (‚Üë) = f ‚àò p' := by
  refine' _root_.isPlot_coinduced_iff.trans ‚ü®fun h => Or.elim h (fun ‚ü®y,hy‚ü© x => _) id,Or.inr‚ü©
  let ‚ü®x',hx'‚ü© := hf y
  exact ‚ü®_,isOpen_univ,mem_univ x,fun _ => x',dsmooth_const,funext fun x => hy ‚ñ∏ hx' ‚ñ∏ rfl‚ü©

open Classical in
/-- The D-topology of the coinduced diffeology agrees with the coinduced topology.
  TODO: refactor definitions to make this a definitional equality. -/
theorem dTop_coinduced_comm {X Y : Type*} {dX : DiffeologicalSpace X} {f : X ‚Üí Y} :
    DTop[dX.coinduced f] = DTop[dX].coinduced f := by
  let _ := dX.coinduced f; let _ := @DTop X _
  let hf : DSmooth f := by rw [dsmooth_iff_coinduced_le]
  refine' le_antisymm (TopologicalSpace.le_def.2 fun u hu => _) _
  ¬∑ rw [isOpen_iff_preimages_plots]; intro n p hp
    rw [isOpen_coinduced] at hu
    obtain ‚ü®y,hy‚ü© | hp := isPlot_coinduced_iff.1 hp
    ¬∑ by_cases h : y ‚àà u; all_goals simp [hy,h]
    ¬∑ refine' isOpen_iff_mem_nhds.2 fun x hx => _
      let ‚ü®v,hv,hxv,p',hp',hp‚ü© := hp x
      refine' mem_nhds_iff.2 ‚ü®_,inter_subset_right v _,_,hxv,hx‚ü©
      rw [‚ÜêSubtype.image_preimage_val,‚Üêpreimage_comp,hp,preimage_comp]
      exact hv.isOpenMap_subtype_val _ (hu.preimage (hv.dTopCompatible.dTop_eq ‚ñ∏ hp'.continuous))
  ¬∑ exact continuous_iff_coinduced_le.1 <| hf.continuous

end DTop

section Quotient

variable {X Y Z : Type*} [DiffeologicalSpace X] [DiffeologicalSpace Y] [DiffeologicalSpace Z]
  {r : X ‚Üí X ‚Üí Prop} {s : Setoid X}

theorem subduction_quot_mk : Subduction (@Quot.mk X r) :=
  ‚ü®Quot.exists_rep, rfl‚ü©

theorem dsmooth_quot_mk : DSmooth (@Quot.mk X r) :=
  dsmooth_coinduced_rng

theorem dsmooth_quot_lift {f : X ‚Üí Y} (hr : ‚àÄ a b, r a b ‚Üí f a = f b) (h : DSmooth f) :
    DSmooth (Quot.lift f hr : Quot r ‚Üí Y) :=
  dsmooth_coinduced_dom.2 h

theorem subduction_quotient_mk' : Subduction (@Quotient.mk' X s) :=
  subduction_quot_mk

theorem dsmooth_quotient_mk' : DSmooth (@Quotient.mk' X s) :=
  dsmooth_coinduced_rng

theorem DSmooth.quotient_lift {f : X ‚Üí Y} (h : DSmooth f) (hs : ‚àÄ a b, a ‚âà b ‚Üí f a = f b) :
    DSmooth (Quotient.lift f hs : Quotient s ‚Üí Y) :=
  dsmooth_coinduced_dom.2 h

theorem DSmooth.quotient_liftOn' {f : X ‚Üí Y} (h : DSmooth f)
    (hs : ‚àÄ a b, @Setoid.r _ s a b ‚Üí f a = f b) :
    DSmooth (fun x => Quotient.liftOn' x f hs : Quotient s ‚Üí Y) :=
  h.quotient_lift hs

theorem DSmooth.quotient_map' {t : Setoid Y} {f : X ‚Üí Y} (hf : DSmooth f)
    (H : (s.r ‚áí t.r) f f) : DSmooth (Quotient.map' f H) :=
  (dsmooth_quotient_mk'.comp hf).quotient_lift _

/-- The plots of the quotient diffeology are precicely those that locally lift to plots. -/
theorem isPlot_quot_iff {n : ‚Ñï} {p : Eucl n ‚Üí Quot r} : IsPlot p ‚Üî ‚àÄ x : Eucl n,
    ‚àÉ u, IsOpen u ‚àß x ‚àà u ‚àß ‚àÉ p' : u ‚Üí X, DSmooth p' ‚àß p ‚àò (‚Üë) = (@Quot.mk X r) ‚àò p' :=
  (surjective_quot_mk r).isPlot_coinduced_iff

/-- The plots of the quotient diffeology are precicely those that locally lift to plots. -/
theorem isPlot_quotient_iff {n : ‚Ñï} {p : Eucl n ‚Üí Quotient s} : IsPlot p ‚Üî ‚àÄ x : Eucl n,
    ‚àÉ u, IsOpen u ‚àß x ‚àà u ‚àß ‚àÉ p' : u ‚Üí X, DSmooth p' ‚àß p ‚àò (‚Üë) = (@Quotient.mk X s) ‚àò p' :=
  Function.Surjective.isPlot_coinduced_iff (by exact Quot.exists_rep)

instance [TopologicalSpace X] [DTopCompatible X] : DTopCompatible (Quot r) :=
  ‚ü®dTop_eq X ‚ñ∏ dTop_coinduced_comm‚ü©

instance [TopologicalSpace X] [DTopCompatible X] : DTopCompatible (Quotient s) :=
  ‚ü®dTop_eq X ‚ñ∏ dTop_coinduced_comm‚ü©

end Quotient

section Prod

variable {X Y Z W Œµ Œ∂: Type*} [DiffeologicalSpace X] [DiffeologicalSpace Y] [DiffeologicalSpace Z]
  [DiffeologicalSpace W] [DiffeologicalSpace Œµ] [DiffeologicalSpace Œ∂]

@[simp]
theorem dsmooth_prod_mk {f : X ‚Üí Y} {g : X ‚Üí Z} :
    DSmooth (fun x => (f x,g x)) ‚Üî DSmooth f ‚àß DSmooth g :=
  dsmooth_inf_rng

theorem dsmooth_fst : DSmooth (@Prod.fst X Y) :=
  (dsmooth_prod_mk.1 dsmooth_id).1

@[fun_prop]
theorem DSmooth.fst {f : X ‚Üí Y √ó Z} (hf : DSmooth f) : DSmooth fun x => (f x).1 :=
  dsmooth_fst.comp hf

theorem DSmooth.fst' {f : X ‚Üí Z} (hf : DSmooth f) : DSmooth fun x : X √ó Y => f x.fst :=
  hf.comp dsmooth_fst

theorem dsmooth_snd : DSmooth (@Prod.snd X Y) :=
  (dsmooth_prod_mk.1 dsmooth_id).2

@[fun_prop]
theorem DSmooth.snd {f : X ‚Üí Y √ó Z} (hf : DSmooth f) : DSmooth fun x => (f x).2 :=
  dsmooth_snd.comp hf

theorem DSmooth.snd' {f : Y ‚Üí Z} (hf : DSmooth f) : DSmooth fun x : X √ó Y => f x.snd :=
  hf.comp dsmooth_snd

@[fun_prop]
theorem DSmooth.prod_mk {f : Z ‚Üí X} {g : Z ‚Üí Y} (hf : DSmooth f) (hg : DSmooth g) :
    DSmooth fun x => (f x, g x) :=
  dsmooth_prod_mk.2 ‚ü®hf, hg‚ü©

theorem DSmooth.Prod.mk (x : X) : DSmooth fun y : Y => (x, y) :=
  dsmooth_const.prod_mk dsmooth_id

theorem DSmooth.Prod.mk_left (y : Y) : DSmooth fun x : X => (x, y) :=
  dsmooth_id.prod_mk dsmooth_const

theorem DSmooth.comp‚ÇÇ {g : X √ó Y ‚Üí Z} (hg : DSmooth g) {e : W ‚Üí X} (he : DSmooth e)
    {f : W ‚Üí Y} (hf : DSmooth f) : DSmooth fun w => g (e w, f w) :=
  hg.comp <| he.prod_mk hf

theorem DSmooth.comp‚ÇÉ {g : X √ó Y √ó Z ‚Üí Œµ} (hg : DSmooth g) {e : W ‚Üí X} (he : DSmooth e)
    {f : W ‚Üí Y} (hf : DSmooth f) {k : W ‚Üí Z} (hk : DSmooth k) :
    DSmooth fun w => g (e w, f w, k w) :=
  hg.comp‚ÇÇ he <| hf.prod_mk hk

theorem DSmooth.comp‚ÇÑ {g : X √ó Y √ó Z √ó Œ∂ ‚Üí Œµ} (hg : DSmooth g) {e : W ‚Üí X} (he : DSmooth e)
    {f : W ‚Üí Y} (hf : DSmooth f) {k : W ‚Üí Z} (hk : DSmooth k) {l : W ‚Üí Œ∂}
    (hl : DSmooth l) : DSmooth fun w => g (e w, f w, k w, l w) :=
  hg.comp‚ÇÉ he hf <| hk.prod_mk hl

theorem DSmooth.prod_map {f : Z ‚Üí X} {g : W ‚Üí Y} (hf : DSmooth f) (hg : DSmooth g) :
    DSmooth fun p : Z √ó W => (f p.1, g p.2) :=
  hf.fst'.prod_mk hg.snd'

/-- A version of `dsmooth_inf_dom_left` for binary functions -/
theorem dsmooth_inf_dom_left‚ÇÇ {X Y Z} {f : X ‚Üí Y ‚Üí Z} {dX dX' : DiffeologicalSpace X}
    {dY dY' : DiffeologicalSpace Y} {dZ : DiffeologicalSpace Z}
    (h : by haveI := dX; haveI := dY; exact DSmooth fun p : X √ó Y => f p.1 p.2) : by
    haveI := dX ‚äì dX'; haveI := dY ‚äì dY'; exact DSmooth fun p : X √ó Y => f p.1 p.2 := by
  have ha := @dsmooth_inf_dom_left _ _ dX dX dX' id (@dsmooth_id _ (id _))
  have hb := @dsmooth_inf_dom_left _ _ dY dY dY' id (@dsmooth_id _ (id _))
  have h_dsmooth_id := @DSmooth.prod_map _ _ _ _ dX dY (dX ‚äì dX') (dY ‚äì dY') _ _ ha hb
  exact @DSmooth.comp _ _ _ (id _) (id _) _ _ _ h h_dsmooth_id

/-- A version of `dsmooth_inf_dom_right` for binary functions -/
theorem dsmooth_inf_dom_right‚ÇÇ {X Y Z} {f : X ‚Üí Y ‚Üí Z} {dX dX' : DiffeologicalSpace X}
    {dY dY' : DiffeologicalSpace Y} {dZ : DiffeologicalSpace Z}
    (h : by haveI := dX'; haveI := dY'; exact DSmooth fun p : X √ó Y => f p.1 p.2) : by
    haveI := dX ‚äì dX'; haveI := dY ‚äì dY'; exact DSmooth fun p : X √ó Y => f p.1 p.2 := by
  have ha := @dsmooth_inf_dom_right _ _ dX dX' dX' id (@dsmooth_id _ (id _))
  have hb := @dsmooth_inf_dom_right _ _ dY dY' dY' id (@dsmooth_id _ (id _))
  have h_dsmooth_id := @DSmooth.prod_map _ _ _ _ dX' dY' (dX ‚äì dX') (dY ‚äì dY') _ _ ha hb
  exact @DSmooth.comp _ _ _ (id _) (id _) _ _ _ h h_dsmooth_id

/-- A version of `dsmooth_sInf_dom` for binary functions -/
theorem dsmooth_sInf_dom‚ÇÇ {X Y Z} {f : X ‚Üí Y ‚Üí Z} {DX : Set (DiffeologicalSpace X)}
    {DY : Set (DiffeologicalSpace Y)} {tX : DiffeologicalSpace X} {tY : DiffeologicalSpace Y}
    {tc : DiffeologicalSpace Z} (hX : tX ‚àà DX) (hY : tY ‚àà DY)
    (hf : DSmooth fun p : X √ó Y => f p.1 p.2) : by
    haveI := sInf DX; haveI := sInf DY;
      exact @DSmooth _ _ _ tc fun p : X √ó Y => f p.1 p.2 := by
  have hX := dsmooth_sInf_dom hX dsmooth_id
  have hY := dsmooth_sInf_dom hY dsmooth_id
  have h_dsmooth_id := @DSmooth.prod_map _ _ _ _ tX tY (sInf DX) (sInf DY) _ _ hX hY
  exact @DSmooth.comp _ _ _ (id _) (id _) _ _ _ hf h_dsmooth_id

theorem dsmooth_swap : DSmooth (Prod.swap : X √ó Y ‚Üí Y √ó X) :=
  dsmooth_snd.prod_mk dsmooth_fst

theorem DSmooth.uncurry_left {f : X ‚Üí Y ‚Üí Z} (x : X) (h : DSmooth (Function.uncurry f)) :
    DSmooth (f x) :=
  h.comp (DSmooth.Prod.mk _)

theorem DSmooth.uncurry_right {f : X ‚Üí Y ‚Üí Z} (y : Y) (h : DSmooth (Function.uncurry f)) :
    DSmooth fun a => f a y :=
  h.comp (DSmooth.Prod.mk_left _)

theorem dsmooth_curry {g : X √ó Y ‚Üí Z} (x : X) (h : DSmooth g) : DSmooth (Function.curry g x) :=
  DSmooth.uncurry_left x h

/-- Smooth functions on products are smooth in their first argument -/
theorem DSmooth.curry_left {f : X √ó Y ‚Üí Z} (hf : DSmooth f) {y : Y} :
    DSmooth fun x ‚Ü¶ f (x, y) :=
  hf.comp (dsmooth_id.prod_mk dsmooth_const)
alias DSmooth.along_fst := DSmooth.curry_left

/-- Smooth functions on products are smooth in their second argument -/
theorem DSmooth.curry_right {f : X √ó Y ‚Üí Z} (hf : DSmooth f) {x : X} :
    DSmooth fun y ‚Ü¶ f (x, y) :=
  hf.comp (dsmooth_const.prod_mk dsmooth_id)
alias DSmooth.along_snd := DSmooth.curry_right

theorem IsPlot.prod {n} {p : Eucl n ‚Üí X} {p' : Eucl n ‚Üí Y} (hp : IsPlot p) (hp' : IsPlot p') :
    IsPlot (fun x => (p x,p' x)) :=
  (hp.dsmooth.prod_mk hp'.dsmooth).isPlot

theorem isPlot_prod_iff {n} {p : Eucl n ‚Üí X √ó Y} :
    IsPlot p ‚Üî IsPlot (fun x => (p x).1) ‚àß IsPlot (fun x => (p x).2) :=
  ‚ü®fun hp => ‚ü®hp.dsmooth.fst.isPlot,hp.dsmooth.snd.isPlot‚ü©,fun h => h.1.prod h.2‚ü©

/-- A product of induced diffeologies is induced by the product map. -/
theorem DiffeologicalSpace.prod_induced_induced (f : X ‚Üí Y) (g : Z ‚Üí W) :
    @instDiffeologicalSpaceProd X Z (induced f ‚Äπ_‚Ä∫) (induced g ‚Äπ_‚Ä∫) =
      induced (fun p => (f p.1, g p.2)) instDiffeologicalSpaceProd := by
  delta instDiffeologicalSpaceProd; simp_rw [induced_inf, induced_compose]; rfl

/-- The first projection in a product of diffeological spaces is a subduction. -/
theorem subduction_fst [Nonempty Y] : Subduction (@Prod.fst X Y) := by
  let y : Y := Nonempty.some inferInstance
  have h : Function.LeftInverse (@Prod.fst X Y) fun x => (x,y) := fun _ => rfl
  exact h.subduction dsmooth_fst dsmooth_id.curry_left

/-- The second projection in a product of diffeological spaces is a subduction. -/
theorem subduction_snd [Nonempty X] : Subduction (@Prod.snd X Y) := by
  let x : X := Nonempty.some inferInstance
  have h : Function.LeftInverse (@Prod.snd X Y) fun y => (x,y) := fun _ => rfl
  exact h.subduction dsmooth_snd dsmooth_id.curry_right

theorem Induction.prod_map {f : X ‚Üí Y} {g : Z ‚Üí W} (hf : Induction f) (hg : Induction g) :
    Induction (Prod.map f g) :=
  ‚ü®hf.1.Prod_map hg.1,by rw [hf.2,hg.2,DiffeologicalSpace.prod_induced_induced f g]; rfl‚ü©

@[simp]
theorem induction_const_prod {x : X} {f : Y ‚Üí Z} :
    (Induction fun y => (x, f y)) ‚Üî Induction f := by
  refine' and_congr ((Prod.mk.inj_left x).of_comp_iff f) _
  simp_rw [instDiffeologicalSpaceProd, DiffeologicalSpace.induced_inf,
    DiffeologicalSpace.induced_compose, Function.comp,
    DiffeologicalSpace.induced_const, top_inf_eq]

@[simp]
theorem induction_prod_const {y : Y} {f : X ‚Üí Z} :
    (Induction fun x => (f x, y)) ‚Üî Induction f := by
  refine' and_congr ((Prod.mk.inj_right y).of_comp_iff f) _
  simp_rw [instDiffeologicalSpaceProd, DiffeologicalSpace.induced_inf,
    DiffeologicalSpace.induced_compose, Function.comp,
    DiffeologicalSpace.induced_const, inf_top_eq]

theorem induction_graph {f : X ‚Üí Y} (hf : DSmooth f) : Induction fun x => (x, f x) :=
  Induction.of_comp' (dsmooth_id.prod_mk hf) dsmooth_fst induction_id

theorem induction_prod_mk (x : X) : Induction (Prod.mk x : Y ‚Üí X √ó Y) :=
  induction_const_prod.2 induction_id

theorem induction_prod_mk_left (y : X) : Induction (fun x : X => (x, y)) :=
  induction_prod_const.2 induction_id

/-- The D-topology of the product diffeology is at least as fine as the product of
  the D-topologies.
  TODO: figure out nice conditions under which this is an equality. -/
theorem dTop_prod_le_prod_dTop :
    (DTop : TopologicalSpace (X √ó Y)) ‚â§ @instTopologicalSpaceProd _ _ DTop DTop :=
  continuous_id_iff_le.1 ((@continuous_prod_mk _ X Y DTop DTop DTop _ _).2
    ‚ü®dsmooth_fst.continuous,dsmooth_snd.continuous‚ü©)

end Prod

section ULift

variable {X : Type u} [DiffeologicalSpace X]

theorem dsmooth_uLift_down : DSmooth (ULift.down : ULift.{v, u} X ‚Üí X) :=
  dsmooth_induced_dom

theorem dsmooth_uLift_up : DSmooth (ULift.up : X ‚Üí ULift.{v, u} X) :=
  dsmooth_induced_rng.2 dsmooth_id

theorem induction_uLift_down : Induction (ULift.down : ULift.{v, u} X ‚Üí X) :=
  ‚ü®ULift.down_injective,rfl‚ü©

-- TODO: ulift discrete diffeologies once instance is available

end ULift
