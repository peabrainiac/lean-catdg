import Mathlib.CategoryTheory.Limits.Types
import Mathlib.CategoryTheory.Closed.Cartesian
import Mathlib.Topology.Category.TopCat.Basic
import Orbifolds.Diffeology.DDiffeomorph
import Orbifolds.Diffeology.Continuous
import Orbifolds.ForMathlib.DeltaGenerated

/-!
# Category of diffeological spaces
The category of diffeological spaces and smooth maps.
Adapted from `Mathlib.Topology.Category.TopCat.Basic`.

Main definitions / results:
* `DiffSp`: the category of diffeological spaces and smooth maps.
* `forget DiffSp`: the forgetful functor `DiffSp â¥¤ Type`,
  provided through a `ConcreteCategory`-instance on `DiffSp`.
* `DiffSp.discrete`, `DiffSp.indiscrete`: the functors `Type â¥¤ DiffSp` giving each type the
  discrete/indiscrete diffeology.
* `DiffSp.discreteForgetAdj`, `DiffSp.forgetIndiscreteAdj`: the adjunctions
  `discrete âŠ£ forget âŠ£ indiscrete`.
* `DiffSp.dTop`, `DiffSp.diffToDeltaGenerated`, `DiffSp.topToDiff`,
  `DiffSp.deltaGeneratedToDiff`: the functors between `DiffSp`, `DeltaGenerated` and
  `TopCat` given by the D-topology and continuous diffeology.
* `DiffSp.dTopAdj`, `DiffSp.dTopAdj'`: the adjunctions between those.
* `DiffSp.hasLimits`, `DiffSp.hasColimits`: `DiffSp` is complete and cocomplete.
* `DiffSp.forgetPreservesLimits`, `DiffSp.forgetPreservesColimits`: the forgetful functor
  `DiffSp â¥¤ Type` preserves limits and colimits.
-/

open CategoryTheory

open Topology

universe u v

/-!
### DiffSp

Basic definitions and lemmas about the category of diffeological spaces.
-/

section Basic

@[to_additive existing DiffSp]
def DiffSp : Type (u + 1) :=
  Bundled DiffeologicalSpace

namespace DiffSp

instance bundledHom : BundledHom @DSmoothMap where
  toFun := @DSmoothMap.toFun
  id := @DSmoothMap.id
  comp := @DSmoothMap.comp

deriving instance LargeCategory for DiffSp

instance concreteCategory : ConcreteCategory DiffSp :=
  inferInstanceAs <| ConcreteCategory (Bundled DiffeologicalSpace)

instance : CoeSort DiffSp Type* where
  coe X := X.Î±

instance diffeologicalSpaceUnbundled (X : DiffSp) : DiffeologicalSpace X :=
  X.str

instance instFunLike (X Y : DiffSp) : FunLike (X âŸ¶ Y) X Y :=
  inferInstanceAs <| FunLike (DSmoothMap X Y) X Y

-- TODO DSmoothMapClass-Instanz

lemma id_app (X : DiffSp) (x : â†‘X) : (ðŸ™ X : X âŸ¶ X) x = x := rfl

lemma comp_app {X Y Z : DiffSp} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (x : X) :
    (f â‰« g : X â†’ Z) x = g (f x) := rfl

@[simp]
lemma coe_id (X : DiffSp) : (ðŸ™ X : X â†’ X) = id := rfl

@[simp]
lemma coe_comp {X Y Z : DiffSp} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
    (f â‰« g : X â†’ Z) = g âˆ˜ f := rfl

@[simp]
lemma hom_inv_id_apply {X Y : DiffSp} (f : X â‰… Y) (x : X) : f.inv (f.hom x) = x :=
  DFunLike.congr_fun f.hom_inv_id x

@[simp]
lemma inv_hom_id_apply {X Y : DiffSp} (f : X â‰… Y) (y : Y) : f.hom (f.inv y) = y :=
  DFunLike.congr_fun f.inv_hom_id y

/-- Construct a bundled space from the underlying type and the typeclass. -/
def of (X : Type u) [DiffeologicalSpace X] : DiffSp :=
  âŸ¨X, inferInstanceâŸ©

@[instance] abbrev diffeologicalSpace_forget
    (X : DiffSp) : DiffeologicalSpace <| (forget DiffSp).obj X :=
  X.str

@[simp]
theorem coe_of (X : Type u) [DiffeologicalSpace X] : (of X : Type u) = X := rfl

-- TODO `coe_of_of`?

instance inhabited : Inhabited DiffSp :=
  âŸ¨Empty,âŠ¥âŸ©

/-- The functor equipping each type with the discrete diffeology. -/
def discrete : Type u â¥¤ DiffSp.{u} where
  obj X := âŸ¨X,âŠ¥âŸ©
  map f := âŸ¨f,dsmooth_botâŸ©

/-- The functor equipping each type with the indiscrete diffeology. -/
def indiscrete : Type u â¥¤ DiffSp.{u} where
  obj X := âŸ¨X,âŠ¤âŸ©
  map f := âŸ¨f,dsmooth_topâŸ©

/-- Adjunction `discrete âŠ£ forget`, adapted from
  `Mathlib.Topology.Category.TopCat.Adjunctions`. -/
@[simps! unit counit]
def discreteForgetAdj : discrete âŠ£ forget DiffSp.{u} :=
  Adjunction.mkOfUnitCounit
    { unit := { app := fun X => id }
      counit := { app := fun X => âŸ¨id, dsmooth_botâŸ© } }

/-- Adjunction `forget âŠ£ indiscrete`, adapted from
  `Mathlib.Topology.Category.TopCat.Adjunctions`. -/
@[simps! unit counit]
def forgetIndiscreteAdj : forget DiffSp.{u} âŠ£ indiscrete :=
  Adjunction.mkOfUnitCounit
    { unit := { app := fun X => âŸ¨id, dsmooth_topâŸ© }
      counit := { app := fun X => id } }

instance : Functor.IsRightAdjoint (forget DiffSp.{u}) :=
  âŸ¨_, âŸ¨discreteForgetAdjâŸ©âŸ©

instance : Functor.IsLeftAdjoint (forget DiffSp.{u}) :=
  âŸ¨_, âŸ¨forgetIndiscreteAdjâŸ©âŸ©

/-- The functor sending each diffeological spaces to its D-topology. -/
def dTop : DiffSp.{u} â¥¤ TopCat.{u} where
  obj X := âŸ¨X,DTopâŸ©
  map f := âŸ¨f,f.dsmooth.continuousâŸ©

/-- The functor sending each diffeological space to its D-topology, as a delta-generated
  space. -/
def diffToDeltaGenerated : DiffSp.{u} â¥¤ DeltaGenerated.{u} where
  obj X := âŸ¨âŸ¨X,DTopâŸ©,inferInstanceâŸ©
  map f := âŸ¨f,f.dsmooth.continuousâŸ©

/-- The functor equipping each topological space with the continuous diffeology. -/
def topToDiff : TopCat.{u} â¥¤ DiffSp.{u} where
  obj X := of (withContinuousDiffeology X)
  map f := âŸ¨f,f.2.dsmoothâŸ©

/-- The functor equipping each delta-generated space with the continuous diffeology. -/
def deltaGeneratedToDiff : DeltaGenerated.{u} â¥¤ DiffSp.{u} where
  obj X := of (withContinuousDiffeology X)
  map f := âŸ¨f.1,f.2.dsmoothâŸ©

/-- Adjunction between the D-topology and continuous diffeology as functors between
  `DiffSp` and `TopCat`. -/
def dTopAdj : dTop âŠ£ topToDiff :=
  Adjunction.mkOfUnitCounit {
    unit := { app := fun X => âŸ¨id,dsmooth_id.continuous.dsmooth'âŸ© }
    counit := { app := fun X => âŸ¨id,continuous_iff_coinduced_le.mpr deltaGenerated_leâŸ© } }

/-- Adjunction between the D-topology and continuous diffeology as functors between
  `DiffSp` and `DeltaGenerated`. -/
def dTopAdj' : diffToDeltaGenerated âŠ£ deltaGeneratedToDiff :=
  Adjunction.mkOfUnitCounit {
    unit := { app := fun X => âŸ¨id,dsmooth_id.continuous.dsmooth'âŸ© }
    counit := { app := fun X => âŸ¨id,continuous_iff_coinduced_le.mpr
      dTop_continuousDiffeology_eq_self.leâŸ© } }

/-- The D-topology functor `DiffSp â¥¤ TopCat` is a left-adjoint. -/
instance : Functor.IsLeftAdjoint (dTop.{u}) :=
  âŸ¨_, âŸ¨dTopAdjâŸ©âŸ©

/-- The D-topology functor `DiffSp â¥¤ DeltaGenerated` is a left-adjoint. -/
instance : Functor.IsLeftAdjoint (diffToDeltaGenerated.{u}) :=
  âŸ¨_, âŸ¨dTopAdj'âŸ©âŸ©

end DiffSp

end Basic

namespace DiffSp

/-!
### Limits and colimits

The category of diffeological spaces is complete and cocomplete, and the forgetful functor
preserves all limits and colimits. Adapted from `Mathlib.Topology.Category.TopCat.Limits`.
-/
section Limits

open CategoryTheory.Limits

variable {J : Type v} [SmallCategory J]

local notation "forget" => forget DiffSp

/-- A specific choice of limit cone for any `F : J â¥¤ DiffSp`. -/
def limitCone (F : J â¥¤ DiffSp.{max v u}) : Cone F where
  pt := of { u : (j : J) â†’ F.obj j | âˆ€ {i j : J} (f : i âŸ¶ j), F.map f (u i) = u j }
  Ï€ :=
    { app := fun j => âŸ¨fun u => u.val j,DSmooth.comp (dsmooth_apply _) (dsmooth_subtype_val)âŸ©
      naturality := fun X Y f => by
        dsimp [Category.id_comp]
        exact DSmoothMap.ext fun a => (a.2 f).symm }

/-- `DiffSp.limitCone F` is actually a limit cone for the given `F : J â¥¤ DiffSp`. -/
def limitConeIsLimit (F : J â¥¤ DiffSp.{max v u}) : IsLimit (limitCone.{u,v} F) where
  lift S :=
    âŸ¨fun x => âŸ¨fun j => S.Ï€.app _ x, fun f => by dsimp; exact S.w f â–¸ rflâŸ©,
    DSmooth.subtype_mk (dsmooth_pi fun j => (S.Ï€.app j).2) fun x i j f => by
      dsimp; exact S.w f â–¸ rflâŸ©
  fac S j := by dsimp [limitCone]; rfl
  uniq S m h := DSmoothMap.ext fun a => Subtype.ext <| by simp_rw [â† h]; rfl

instance hasLimitsOfSize : HasLimitsOfSize.{v,v} DiffSp.{max u v} where
  has_limits_of_shape _ := âŸ¨fun F => HasLimit.mk âŸ¨limitCone.{u,v} F,limitConeIsLimit FâŸ©âŸ©

/-- `DiffSp` has all limits, i.e. it is complete. -/
instance hasLimits : HasLimits DiffSp.{u} :=
  hasLimitsOfSize.{u,u}

noncomputable instance forgetPreservesLimitsOfSize :
    PreservesLimitsOfSize.{v,v} (forget : DiffSp.{max v u} â¥¤ _) :=
  âŸ¨âŸ¨fun {F} => preservesLimit_of_preserves_limit_cone (limitConeIsLimit.{u,v} F)
      (Types.limitConeIsLimit.{v,u} (F â‹™ forget))âŸ©âŸ©

/-- The forgetful functor `DiffSp â¥¤ Type` preserves all limits. -/
noncomputable instance forgetPreservesLimits : PreservesLimits (forget : DiffSp.{u} â¥¤ _) :=
  forgetPreservesLimitsOfSize.{u,u}

/-- A specific choice of colimit cocone for any `F : J â¥¤ DiffSp`. -/
noncomputable def colimitCocone (F : J â¥¤ DiffSp.{max v u}) : Cocone F where
  pt := âŸ¨(Types.TypeMax.colimitCocone.{v,u} (F â‹™ forget)).pt,
          â¨† j, (F.obj j).str.coinduced ((Types.TypeMax.colimitCocone (F â‹™ forget)).Î¹.app j)âŸ©
  Î¹ :=
    { app := fun j =>
        âŸ¨(Types.TypeMax.colimitCocone (F â‹™ forget)).Î¹.app j, dsmooth_iff_coinduced_le.mpr <|
          le_iSup (fun j => DiffeologicalSpace.coinduced
            ((Types.TypeMax.colimitCocone (F â‹™ forget)).Î¹.app j) (F.obj j).str) jâŸ©
      naturality := fun _ _ f =>
        DSmoothMap.coe_injective ((Types.TypeMax.colimitCocone (F â‹™ forget)).Î¹.naturality f) }


/-- `DiffSp.colimitCocone F` is actually a colimit cocone for the given `F : J â¥¤ DiffSp`. -/
def colimitCoconeIsColimit (F : J â¥¤ DiffSp.{max v u}) : IsColimit (colimitCocone F) := by
  refine IsColimit.ofFaithful forget (Types.TypeMax.colimitCoconeIsColimit.{v,u} _) (fun s =>
      âŸ¨Quot.lift (fun p => (Functor.mapCocone forget s).Î¹.app p.fst p.snd) ?_, ?_âŸ©) fun s => rfl
  Â· intro _ _ âŸ¨_, hâŸ©; simp [h,â†comp_apply',s.Î¹.naturality]
  Â· exact dsmooth_iff_le_induced.mpr
      (iSup_le fun j => DiffeologicalSpace.coinduced_le_iff_le_induced.mp <|
        DiffeologicalSpace.coinduced_compose.symm â–¸ (s.Î¹.app j).dsmooth.coinduced_le)

instance hasColimitsOfSize : HasColimitsOfSize.{v,v} DiffSp.{max v u} where
  has_colimits_of_shape _ := âŸ¨fun F => HasColimit.mk âŸ¨colimitCocone F,colimitCoconeIsColimit FâŸ©âŸ©

/-- `DiffSp` has all colimits, i.e. it is cocomplete. -/
instance hasColimits : HasColimits DiffSp.{u} :=
  hasColimitsOfSize.{u,u}

noncomputable instance forgetPreservesColimitsOfSize :
    PreservesColimitsOfSize.{v,v} (forget : DiffSp.{max v u} â¥¤ _) :=
  âŸ¨âŸ¨fun {F} => preservesColimit_of_preserves_colimit_cocone (colimitCoconeIsColimit.{u,v} F)
    (Types.TypeMax.colimitCoconeIsColimit.{v,u} (F â‹™ forget))âŸ©âŸ©

/-- The forgetful functor `DiffSp â¥¤ Type` preserves all colimits. -/
noncomputable instance forgetPreservesColimits : PreservesColimits (forget : DiffSp.{u} â¥¤ _) :=
  forgetPreservesColimitsOfSize.{u,u}

end Limits

/-!
### Products
Products in `DiffSp` are given by the usual products of spaces.
Adapted from `Mathlib.CategoryTheory.Limits.Shapes.Types`.
-/
section BinaryProducts

open Limits WalkingPair

/-- The product space `X Ã— Y` as a cone. -/
def binaryProductCone (X Y : DiffSp.{u}) : BinaryFan X Y :=
  BinaryFan.mk (P := of (X Ã— Y)) âŸ¨_,dsmooth_fstâŸ© âŸ¨_,dsmooth_sndâŸ©

/-- `DiffSp.binaryProductCone X Y` is actually a limiting cone. -/
def binaryProductLimit (X Y : DiffSp.{u}) : IsLimit (binaryProductCone X Y) where
  lift (s : BinaryFan X Y) := âŸ¨_,s.fst.dsmooth.prod_mk s.snd.dsmoothâŸ©
  fac _ j := Discrete.recOn j fun j => by cases' j <;> rfl
  uniq s f w := DSmoothMap.ext fun x => Prod.ext
    (congrFun (congrArg DSmoothMap.toFun (w âŸ¨leftâŸ©)) x)
    (congrFun (congrArg DSmoothMap.toFun (w âŸ¨rightâŸ©)) x)

/-- The functor taking `X`, `Y` to the product space `X Ã— Y`. -/
def binaryProductFunctor : DiffSp.{u} â¥¤ DiffSp.{u} â¥¤ DiffSp.{u} where
  obj X := {
    obj := fun Y => of (X Ã— Y)
    map := fun {Y Y'} f => âŸ¨_,dsmooth_id.prod_map f.dsmoothâŸ© }
  map {X Y} f := {
    app := fun Z => âŸ¨_,f.dsmooth.prod_map dsmooth_idâŸ©
    naturality := fun {X' Y'} f' => rfl }
  map_id := fun X => rfl
  map_comp := fun {X Y Z} f g => rfl

/-- The explicit products we defined are naturally isomorphic to the products coming from
  the `HasLimits` instance on DiffSp. This is needed because the `HasLimits`
  instance only stores proof that all limits exist, not the explicit constructions,
  so the products derived from it are picked with the axiom of choice. -/
noncomputable def binaryProductIsoProd : binaryProductFunctor.{u} â‰… (prod.functor) := by
  refine' NatIso.ofComponents (fun X => _) (fun _ => _)
  Â· refine' NatIso.ofComponents (fun Y => _) (fun _ => _)
    Â· exact ((limit.isLimit _).conePointUniqueUpToIso (binaryProductLimit X Y)).symm
    Â· apply Limits.prod.hom_ext <;> simp <;> rfl
  Â· ext : 2; apply Limits.prod.hom_ext <;> simp <;> rfl

/-- The one-point space as a cone. -/
def terminalCone : Cone (Functor.empty DiffSp.{u}) where
  pt := âŸ¨PUnit, âŠ¤âŸ©
  Ï€ := (Functor.uniqueFromEmpty _).hom

/-- `DiffSp.terminalCone` is actually limiting. -/
def terminalCodeIsLimit : IsLimit terminalCone where
  lift c := âŸ¨fun _ â†¦ PUnit.unit, dsmooth_constâŸ©

end BinaryProducts

section Cartesian

instance : ChosenFiniteProducts DiffSp where
  product X Y := âŸ¨binaryProductCone X Y, binaryProductLimit X YâŸ©
  terminal := âŸ¨terminalCone, terminalCodeIsLimitâŸ©

/-- `DiffSp` is cartesian-closed. -/
noncomputable instance cartesianClosed : CartesianClosed DiffSp.{u} where
  closed X := âŸ¨{
      obj := fun Y => DiffSp.of (DSmoothMap X Y)
      map := fun f => âŸ¨f.comp,DSmoothMap.dsmooth_comp.curry_rightâŸ©
    }, by exact Adjunction.mkOfHomEquiv {
      homEquiv := fun Y Z => (DDiffeomorph.prodComm.comp_right).toEquiv.trans
        (@DDiffeomorph.curry Y X Z _ _ _).toEquiv
      homEquiv_naturality_left_symm := fun _ _ => rfl
      homEquiv_naturality_right := fun _ _ => rfl
    }âŸ©

end Cartesian

end DiffSp
