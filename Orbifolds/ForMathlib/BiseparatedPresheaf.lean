import Mathlib.CategoryTheory.Closed.Types
import Mathlib.CategoryTheory.Sites.CartesianClosed
import Mathlib.CategoryTheory.Sites.LeftExact
import Orbifolds.ForMathlib.Classifier

/-!
# Biseparated presheaves
On any category with a two Grothendieck topologies `J` and `K`, we define the category `Bisep J K`
of all presheaves that are sheaves with respect to `J` and separated with respect to `K`, and show
that it is a reflective subcategory (TODO). Important examples (though not worked out in this file)
are the categories of diffeological spaces, quasitopological spaces and simplicial complexes.

See https://ncatlab.org/nlab/show/separated+presheaf#biseparated_presheaf and section C.2.2 of
*Sketches of an Elephant*.
-/

universe u v w u‚ÇÇ v‚ÇÇ

open CategoryTheory Category Sheaf GrothendieckTopology

namespace CategoryTheory

variable {C : Type u} [Category.{v} C] (J K : GrothendieckTopology C)

/-- The category of biseparated presheaves with respect to `J` and `K`, i.e. of all presheaves that
are sheaves with respect to `J` and separated with respect to `K`. -/
structure Bisep where
  /-- The underlying presheaf. -/
  val : C·µí·µñ ‚•§ Type w
  /-- The underlying presheaf is a sheaf with respect to `J`. -/
  isSheaf : Presheaf.IsSheaf J val
  /-- The underlying presheaf is separated with respect to `K`. -/
  isSeparated : Presieve.IsSeparated K val

variable {J K}

/-- Morphisms of biseparated presheaves are just morphisms of their underlying presheaves. -/
@[ext]
structure Bisep.Hom (X Y : Bisep J K) where
  /-- The underlying morphism of presheaves. -/
  val : X.val ‚ü∂ Y.val

@[simps id_val comp_val]
instance Bisep.instCategory : Category (Bisep J K) where
  Hom := Hom
  id _ := ‚ü®ùüô _‚ü©
  comp f g := ‚ü®f.val ‚â´ g.val‚ü©
  id_comp _ := Hom.ext <| id_comp _
  comp_id _ := Hom.ext <| comp_id _
  assoc _ _ _ := Hom.ext <| assoc _ _ _

/-- The sheaf (with respect to `J`) underlying a biseparated presheaf. -/
@[simps]
def Bisep.toSheaf (P : Bisep J K) : Sheaf J (Type w) := ‚ü®P.val, P.isSheaf‚ü©

variable (J K) in
/-- The inclusion functor from biseparated presheaves to sheaves. -/
@[simps]
def bisepToSheaf : Bisep J K ‚•§ Sheaf J (Type w) where
  obj P := P.toSheaf
  map f := ‚ü®f.val‚ü©

variable (J K) in
/-- The inclusion functor from biseparated presheaves to sheaves is fully faithful. -/
def fullyFaithfulBisepToSheaf : (bisepToSheaf J K).FullyFaithful where
  preimage f := ‚ü®f.val‚ü©

instance : (bisepToSheaf J K).Full :=
  (fullyFaithfulBisepToSheaf J K).full

instance : (bisepToSheaf J K).Faithful :=
  (fullyFaithfulBisepToSheaf J K).faithful

instance : (bisepToSheaf J K).ReflectsIsomorphisms :=
  (fullyFaithfulBisepToSheaf J K).reflectsIsomorphisms

-- this is currently needed to obtain the instance `HasSheafify J (Type max u v)`.
attribute [local instance] CategoryTheory.Types.instConcreteCategory
attribute [local instance] CategoryTheory.Types.instFunLike

noncomputable def Sheaf.toExpŒ©' (h : J ‚â§ K) (X : Sheaf J (Type max u v)) : X ‚ü∂ X ‚üπ J.Œ©' h :=
  CartesianClosed.curry (J.classifier.œá <| CartesianMonoidalCategory.lift (ùüô X) (ùüô X)) ‚â´
    (exp X).map (J.Œ©ProjectionOfLE h)

/-- Subpresheaves of separated presheaves are themselves separated. -/
lemma Subpresheaf.isSeparated_toPresheaf {C : Type u} [Category.{v, u} C]
    {J : GrothendieckTopology C} {F : Functor C·µí·µñ (Type w)} (G : Subpresheaf F)
    (h : Presieve.IsSeparated J F) : Presieve.IsSeparated J G.toPresheaf := by
  intro X S hS x _ _ hx‚ÇÅ hx‚ÇÇ
  exact Subtype.ext <| h S hS _ _ _ (hx‚ÇÅ.compPresheafMap G.Œπ) (hx‚ÇÇ.compPresheafMap G.Œπ)

open MonoidalCategory in
/-- A more concrete choice of exponential object in presheaf categories. -/
@[simps]
def Functor.chosenExp {C : Type u} [Category.{v} C] (F G : C·µí·µñ ‚•§ Type max u v) :
    C·µí·µñ ‚•§ Type max u v where
  obj X := F ‚äó uliftYoneda.obj X.unop ‚ü∂ G
  map f g := F ‚óÅ uliftYoneda.map f.unop ‚â´ g

lemma uliftYonedaEquiv_naturality_symm {C : Type u} [Category.{v} C] {X Y : C·µí·µñ}
    {F : C·µí·µñ ‚•§ Type max w v} (x : F.obj X) (f : X ‚ü∂ Y) :
    uliftYonedaEquiv.symm (F.map f x) = uliftYoneda.map f.unop ‚â´ uliftYonedaEquiv.symm x := by
  simpa using (congrArg uliftYonedaEquiv.symm <|
    uliftYonedaEquiv_naturality (uliftYonedaEquiv.symm x) f:)

/-- Isomorphism witnessing that in presheaf categories, exponential objects are indeed isomorphic
to `Functor.chosenExp`. -/
noncomputable def Functor.expObjIsoChosenExp {C : Type u} [Category.{v} C]
    (F G : C·µí·µñ ‚•§ Type max u v) : F ‚üπ G ‚âÖ F.chosenExp G :=
  NatIso.ofComponents (fun X ‚Ü¶ {
    hom x := CartesianClosed.uncurry <| uliftYonedaEquiv.symm x
    inv f := uliftYonedaEquiv <| CartesianClosed.curry f
  }) fun f ‚Ü¶ funext fun x ‚Ü¶ (congrArg CartesianClosed.uncurry <|
    uliftYonedaEquiv_naturality_symm x f).trans <| CartesianClosed.uncurry_natural_left _ _

/-- The property of being separated for some presieve is preserved under isomorphisms.
TODO: upstream to mathlib. -/
theorem Presieve.isSeparatedFor_iso {C : Type u} [Category.{v} C]
    {F F' : C·µí·µñ ‚•§ Type w} (i : F ‚âÖ F') {X : C} {R : Presieve X} (hF : R.IsSeparatedFor F) :
    R.IsSeparatedFor F' := by
  intro x t‚ÇÅ t‚ÇÇ ht‚ÇÅ ht‚ÇÇ
  simpa using congrArg (i.hom.app _) <| hF (x.compPresheafMap i.inv) _ _
    (ht‚ÇÅ.compPresheafMap i.inv) (ht‚ÇÇ.compPresheafMap i.inv)

/-- The property of being separated is preserved under isomorphisms.
TODO: upstream to mathlib. -/
theorem Presieve.isSeparated_iso {C : Type u} [Category.{v} C]
    (J : GrothendieckTopology C) {F F' : C·µí·µñ ‚•§ Type w} (i : F ‚âÖ F')
    (hF : Presieve.IsSeparated J F) : Presieve.IsSeparated J F' :=
  fun S hS ‚Ü¶ Presieve.isSeparatedFor_iso i (hF S hS)

open MonoidalCategory in
example {C : Type u} [Category.{v} C] {F G : C·µí·µñ ‚•§ Type max w v} (f : F ‚ü∂ G)
    (H : C·µí·µñ ‚•§ Type max w v) (X : C·µí·µñ) : (f ‚ñ∑ H).app X = Prod.map (f.app X) id := by
  rfl

open CartesianClosed Opposite MonoidalCategory in
lemma Presieve.IsSeparated.exp {C : Type u} [Category.{v} C] {J : GrothendieckTopology C}
    {F : C·µí·µñ ‚•§ Type max u v} (hF : IsSeparated J F) (G : C·µí·µñ ‚•§ Type max u v) :
    IsSeparated J (G ‚üπ F) := by
  /-intro X S hS x x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ
  refine uliftYonedaEquiv.{u,v,u}.symm.injective <| uncurry_injective ?_
  ext Y ‚ü®y, ‚ü®(f : _ ‚ü∂ _)‚ü©‚ü©
  refine (hF _ <| J.pullback_stable f hS).ext fun Z g hg ‚Ü¶ ?_
  refine ((congrFun ((uncurry (uliftYonedaEquiv.symm x‚ÇÅ)).naturality g.op).symm _).trans ?_).trans
    (congrFun ((uncurry (uliftYonedaEquiv.symm x‚ÇÇ)).naturality g.op) _)
  dsimp [uliftYoneda]
  have h := (hx‚ÇÅ _ hg).trans (hx‚ÇÇ _ hg).symm
  replace h := (uliftYonedaEquiv_naturality_symm x‚ÇÅ (g ‚â´ f).op).symm.trans <|
    (congrArg _ h).trans <| uliftYonedaEquiv_naturality_symm x‚ÇÇ (g ‚â´ f).op
  replace h := (uncurry_natural_left _ _).symm.trans <| (congrArg uncurry h).trans <|
    uncurry_natural_left _ _
  have h' := congrFun (NatTrans.congr_app h _) ‚ü®G.map g.op y, ‚ü®ùüô _‚ü©‚ü©
  simp at h'-/
  refine isSeparated_iso J (G.expObjIsoChosenExp F).symm ?_
  intro X S hS x x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ
  dsimp
  ext Y ‚ü®y, ‚ü®(f : _ ‚ü∂ _)‚ü©‚ü©
  refine (hF _ <| J.pullback_stable f hS).ext fun Z g hg ‚Ü¶ ?_
  refine ((congrFun (x‚ÇÅ.naturality g.op).symm _).trans ?_).trans (congrFun (x‚ÇÇ.naturality g.op) _)
  have h := congrFun (NatTrans.congr_app ((hx‚ÇÅ _ hg).trans (hx‚ÇÇ _ hg).symm) _) ‚ü®G.map g.op y, ‚ü®ùüô _‚ü©‚ü©
  refine (congrArg (x‚ÇÅ.app _) ?_).trans <| h.trans <| congrArg (x‚ÇÇ.app _) <| Eq.symm ?_
  all_goals exact Prod.ext rfl <| ULift.ext _ _ (id_comp _).symm

lemma Presheaf.IsSheaf.exp {C : Type u} [Category.{v} C] {J : GrothendieckTopology C} {A : Type u‚ÇÇ}
    [Category.{v‚ÇÇ} A] [HasSheafify J A] [CartesianMonoidalCategory A] [CartesianClosed (C·µí·µñ ‚•§ A)]
    {F : C·µí·µñ ‚•§ A} (hF : IsSheaf J F) (G : C·µí·µñ ‚•§ A) : IsSheaf J (G ‚üπ F) :=
  (Presheaf.isSheaf_of_iso_iff <| Classical.choice (ExponentialIdeal.exp_closed
    (i := sheafToPresheaf J A) ‚ü®‚ü®F, hF‚ü©, ‚ü®Iso.refl _‚ü©‚ü© G).choose_spec).1 (Sheaf.cond _)

-- TODO: move to `Mathlib.CategoryTheory.Sites.Sheaf`
lemma Presieve.isSeparated_of_le {C : Type u} [Category.{v} C] {P : C·µí·µñ ‚•§ Type w}
    {J K : GrothendieckTopology C} (h : J ‚â§ K) (hP : Presieve.IsSeparated K P) :
    Presieve.IsSeparated J P :=
  fun S hS ‚Ü¶ hP S <| h _ hS

variable (J K) in
noncomputable def sheafToBisep : Sheaf J (Type max u v) ‚•§ Bisep J K where
  obj X := {
    val := _
    isSheaf := cond <| image <| X.toExpŒ©' (le_sup_left (b := K))
    isSeparated := Subpresheaf.isSeparated_toPresheaf _ <| by
      have h : Presieve.IsSeparated K (X.1 ‚üπ (J.Œ©' (le_sup_left (b := K))).1) := by
        refine Presieve.IsSeparated.exp (Presieve.isSeparated_of_isSheaf _ _ ?_) _
        exact (isSheaf_iff_isSheaf_of_type _ _).1 <| Presheaf.isSheaf_of_le le_sup_right (J ‚äî K).Œ©.2
      exact fun {_} ‚Ü¶ Presieve.isSeparated_iso K
        (isoSheafify _ <| Presheaf.IsSheaf.exp (Sheaf.cond _) _) h
  }
  map {X Y} f := ‚ü®{
    app Z := by
      simp
      sorry
  }‚ü©

end CategoryTheory
